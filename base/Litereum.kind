// Litereum: a minimal decentralized computer

// A private key is a set of 32 words
Litereum.Private: Type
  Vector<U256, 32>

// A public key is the hash^32 of the private key
Litereum.Public: Type
  Vector<U256, 32>

// An address is the hash of a public key
Litereum.Address: Type
  U256

// A hash is a 256-bit word
Litereum.Hash: Type
  U256

// A signature is a set of 32 words
// - 30 words sign the first 30 bytes of the message hash
// -  2 words sign the checksum
Litereum.Signature: Type
  Vector<U256, 32>

// The 32 bytes to be signed by a signature
Litereum.Summary: Type
  Vector<U8, 32>

// The body of a Litereum Post can hold up to 8 words
Litereum.Body: Type
  Vector<U256,8>

Litereum.Body.deserializer: Deserializer<Litereum.Body>
  Vector.deserializer!(8,U256.deserializer)

Litereum.Body.serializer: Serializer<Litereum.Body>
  Vector.serializer!(8,U256.serializer)

Litereum.Signature.deserializer: Deserializer<Litereum.Signature>
  Vector.deserializer!(32,U256.deserializer)

Litereum.Signature.serializer: Serializer<Litereum.Signature>
  Vector.serializer!(32,U256.serializer)

Litereum.Hash.serializer: Serializer<Litereum.Hash>
  U256.serializer

Litereum.Hash.deserializer: Deserializer<Litereum.Hash>
  U256.deserializer

// Takes the Keccak hash of a list of words
Litereum.Hash.from_words(words: List<U256>): Litereum.Hash
  Crypto.Keccak.hash.words(words)

// Hashes a buffer into a 1024-byte signable string
Litereum.Summary.from_bytes(len_bytes: Pair<U32,Buffer8>): Litereum.Summary
  let vals = Buffer8.to_list(Crypto.Keccak.hash.bytes(len_bytes))
  let csum = 7650 :: U32
  let csum = for i from 2 to List.length!(vals): csum - U8.to_u32(vals[i] <> 0)
  let vals = vals[0] <- U32.to_u8(U32.div(csum, 256))
  let vals = vals[1] <- U32.to_u8(U32.mod(csum, 256))
  let vals = Vector.from_list.length!(32, 0, vals)
  vals

// Converts a list of words to a signable
Litereum.Summary.from_words(words: List<U256>): Litereum.Summary
  Litereum.Summary.from_bytes(Buffer8.from_list.256(words))

// Converts a UTF-8 string to a signable
Litereum.Summary.from_utf8(str: String): Litereum.Summary
  Litereum.Summary.from_bytes(Buffer8.from_hex(String.to_hex(str)))

// Converts a seed to a private key
Litereum.Private.from_seed(seed: U256): Litereum.Private
  Litereum.Private.from_seed.go(32, seed)

Litereum.Private.from_seed.go(len: Nat, seed: U256): Vector<U256,len>
  case len {
    zero:
      Vector.nil!
    succ:
      let head = Litereum.Hash.from_words([U256.add(seed, Nat.to_u256(len))])
      let tail = Litereum.Private.from_seed.go(len.pred, seed)
      Vector.cons<U256,len.pred>(head, tail)
  }!

// Coverts a private key to an address
Litereum.Public.from_private(pvt: Litereum.Private): Litereum.Private
  Litereum.Public.from_private.go(32, pvt)

Litereum.Public.from_private.go(len: Nat, pvt: Vector<U256,len>): Vector<U256,len>
  case len with pvt {
    zero: Vector.nil!
    succ: case pvt {
      cons:
        let hash = pvt.head
        let hash = for i from 0 to 256:
          Litereum.Hash.from_words([hash])
        Vector.cons<U256,len.pred>(hash, Litereum.Public.from_private.go(len.pred, pvt.tail))
    }
  }!

// Converts a public key to an address
Litereum.Address.from_public(pub: Litereum.Public): Litereum.Address
  let vals = Buffer8.from_list.256(Vector.to_list!(32, pub))
  let addr = Buffer8.to_u256(Crypto.Keccak.hash.bytes(vals)@snd)
  addr

// Signs a signable
Litereum.Signature.sign(msg: Litereum.Summary, pvt: Litereum.Private): Litereum.Signature
  Litereum.Signature.sign.go(32, msg, pvt)

Litereum.Signature.sign.go(len: Nat, msg: Vector<U8,len>, pvt: Vector<U256,len>): Vector<U256,len>
  case len with msg pvt {
    zero: case msg pvt {
      nil nil: Vector.nil!
    }
    succ: case msg pvt {
      cons cons:
        let hash = pvt.head
        let hash = for i from 0 to 256 - U8.to_nat(msg.head):
          Litereum.Hash.from_words([hash])
        Vector.cons<U256,len.pred>(hash, Litereum.Signature.sign.go(len.pred, msg.tail, pvt.tail))
    }
  }!

// Verifies if a signature is correct
Litereum.Signature.verify(msg: Litereum.Summary, addr: Litereum.Address, sig: Litereum.Signature): Bool
  let checksums = Litereum.Signature.checksum(msg)
  let sign_addr = Litereum.Signature.recover(msg, sig)
  checksums && U256.eql(addr, sign_addr)

// Checks if the signable checksum is correct
Litereum.Signature.checksum(msg: Litereum.Summary): Bool
  Litereum.Signature.checksum.go(32, msg)

Litereum.Signature.checksum.go(len: Nat, msg: Vector<U8,len>): Bool
  let vals = Vector.to_list<U8>(len, msg)
  let csum = 7650 :: U32
  let csum = for i from 2 to List.length!(vals):
    csum - U8.to_u32(vals[i] <> 0)
  let cs_0 = U8.to_u32(vals[0] <> 0)
  let cs_1 = U8.to_u32(vals[1] <> 0)
  U32.eql(csum, U32.add(U32.mul(cs_0, 256), cs_1))

// Returns the public key that signed a message
Litereum.Signature.recover(msg: Litereum.Summary, sig: Litereum.Signature): Litereum.Address
  Litereum.Address.from_public(Litereum.Signature.recover.go(32, msg, sig))

Litereum.Signature.recover.go(len: Nat, msg: Vector<U8,len>, sig: Vector<U256,len>): Vector<U256,len>
  case len with msg sig {
    zero: Vector.nil!
    succ: case msg sig {
      cons cons cons:
        let hash = sig.head
        let hash = for i from 0 to U8.to_nat(msg.head): Litereum.Hash.from_words([hash])
        Vector.cons<U256,len.pred>(hash, Litereum.Signature.recover.go(len.pred, msg.tail, sig.tail))
    }
  }!

// A term of the Litereum scripting language
type Litereum.Term {
  lam(
    name: String,
    body: Litereum.Term -> Litereum.Term,
  )
  app(
    func: Litereum.Term,
    argm: Litereum.Term,
  )
  var(
    name: String,
  )

  nil
  cons0(
    pred: Litereum.Term,
  )
  cons1(
    pred: Litereum.Term,
  )
  elim(
    bits: Litereum.Term,
    case_nil: Litereum.Term,
    case_cons0: Litereum.Term -> Litereum.Term,
    case_cons1: Litereum.Term -> Litereum.Term,
  )

  end(
    done: Litereum.Term,
  )
  get(
    key: Litereum.Term,
    cont: Litereum.Term -> Litereum.Term,
  )
  set(
    key: Litereum.Term,
    val: Litereum.Term,
    cont: Litereum.Term -> Litereum.Term,
  )
  call(
    message: Litereum.Term,
    recipient: Litereum.Term,
    cont: Litereum.Term -> Litereum.Term,
  )
  install(
    name: Litereum.Term,
    term: Litereum.Term,
    cont: Litereum.Term -> Litereum.Term,
  )
}

type Litereum.Bond {
  new(
    entry: Litereum.Term
    state: Map<Bits>
  )
}

type Litereum.World {
  new(
    bonds: Map<Litereum.Bond>
    terms: Map<Litereum.Term>
  )
}

Litereum.Term.to_bits(
  term: Litereum.Term
): Bits
  case Litereum.Term.reduce(term) as term {
    end: Bits.e
    cons0: Bits.o(Litereum.Term.to_bits(term.pred))
    cons1: Bits.i(Litereum.Term.to_bits(term.pred))
  } default Bits.e

Litereum.Term.from_bits(
  bits: Bits
): Litereum.Term
  case bits {
    e: Litereum.Term.nil
    o: Litereum.Term.cons0(Litereum.Term.from_bits(bits.pred))
    i: Litereum.Term.cons1(Litereum.Term.from_bits(bits.pred))
  }

Litereum.Term.reduce(
  term: Litereum.Term
): Litereum.Term
  case term {
    app: case Litereum.Term.reduce(term.func) as func {
      lam: Litereum.Term.reduce(func.body(term.argm))
    } default term
    elim: case Litereum.Term.reduce(term.bits) as bits {
      nil: Litereum.Term.reduce(term.case_nil)
      cons0: Litereum.Term.reduce(term.case_cons0(bits.pred))
      cons1: Litereum.Term.reduce(term.case_cons1(bits.pred))
    } default term
  } default term
  
Litereum.Term.execute(
  cost: Nat
  user: Litereum.Address
  term: Litereum.Term
  world: Litereum.World
): Pair<Litereum.Term, Litereum.World>
  open world
  let bond = world.bonds{U256.show(user)}
  case Litereum.Term.reduce(term) as term {
    end:
      {term.done, world}
    get: 
      let key = Litereum.Term.to_bits(term.key)
      let cost = cost + 1
      case bond {
        none:
          Litereum.Term.execute(cost, user, term.cont(Litereum.Term.nil), world)
        some:
          use bond = bond.value
          let val = Litereum.Term.from_bits(bond.state{Bits.show(key)} <> Bits.e)
          Litereum.Term.execute(cost, user, term.cont(val), world)
      }
    set:
      let key = Litereum.Term.to_bits(term.key)
      let val = Litereum.Term.to_bits(term.val)
      let cost = cost + 1
      let world = case bond {
        none:
          world
        some: 
          use bond = bond.value
          let state = bond.state{Bits.show(key)} <- val
          let bond = bond@state <- state
          let world.bonds = world.bonds{U256.show(user)} <- bond
          world@bonds <- world.bonds
      }
      Litereum.Term.execute(cost, user, term.cont(Litereum.Term.nil), world)
      call:
        Litereum.Term.execute(cost, user, term.cont(Litereum.Term.nil), world)
      install:
        Litereum.Term.execute(cost, user, term.cont(Litereum.Term.nil), world)
  } default {Litereum.Term.nil, world}
