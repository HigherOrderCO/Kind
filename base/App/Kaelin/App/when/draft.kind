App.Kaelin.App.when.draft(
  players: Map<App.Kaelin.DraftInfo>,
  event: App.Event,
  state: App.Store<App.Kaelin.State>
): IO<Maybe<App.State.local<App.Kaelin.State>>>
  
  case event {
    mouse_click:
      switch String.starts_with(event.id) {
        "C" :
          let coord_nat = String.drop(1, event.id)
          let coord     = App.Kaelin.Coord.Convert.nat_to_axial(Nat.read(coord_nat))
          App.new_post!(state@global@room, App.Kaelin.Event.serialize(App.Kaelin.Event.draft_coord(coord)))
        "H" :
          let heroes_list = List.map!!(Pair.snd!!, Map.to_list!(players))
          let team        = App.Kaelin.Coord.draft.to_team(state@local@user, players) <> App.Kaelin.Team.neutral
          let heroes_map  = App.Kaelin.Hero.info.map
          let hero_name   = String.drop(1, event.id)
          let hero_id     = heroes_map{hero_name}
          let taken       = false
          for player in heroes_list with taken:
            open player
            if App.Kaelin.Team.eql(team, player.team) then
              if U8.eql(player.hero<> 99, hero_id <> 0) then
                true
              else
                taken
            else
              taken
          case hero_id {
            none: App.pass!
            some: 
              if taken then
                App.pass!
              else 
                App.new_post!(state@global@room, App.Kaelin.Event.serialize(App.Kaelin.Event.draft_hero(hero_id.value)))
          }
        "R" : 
          App.new_post!(state@global@room, App.Kaelin.Event.serialize(App.Kaelin.Event.draft_end))
        "T" :
          let player_count = String.drop(1, event.id)
          if String.starts_with(player_count, "3") then
            App.pass!
          else
            let team = String.drop(1, player_count)
            switch String.eql(team){
              "blue": App.new_post!(state@global@room, App.Kaelin.Event.serialize(App.Kaelin.Event.draft_team(0)))
              "red" : App.new_post!(state@global@room, App.Kaelin.Event.serialize(App.Kaelin.Event.draft_team(1)))
            } default App.pass!
    } default App.pass!
  
  } default App.pass!