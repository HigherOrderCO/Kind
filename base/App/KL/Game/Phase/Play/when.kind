App.KL.Game.Phase.Play.when(
  local: App.KL.Game.State.Local
  game: App.KL.Game
  event: App.Event,
): IO<Maybe<App.State.local<App.KL.State>>>
  let room = String.take(16, Crypto.Keccak.hash(local@room))
  open game
  case event {
    frame:
      log("Screen size: " | U32.show(local@screen_size@fst) | ", " | U32.show(local@screen_size@snd))
      let picks = App.KL.Game.Cast.picks_of(local@user, game)
      let preview = local@preview <- (local@preview@picks <- picks)
      let preview = preview@screen_size <- event.info@screen_size
      App.set_local!(App.KL.State.Local.game(preview))
    mouse_move:
      if String.eql("game_screen", event.id) then
        let {x,y} = event.mouse_pos
        let local = local@hud <- App.KL.Game.State.Local.hud.new(none)
        let local = local@mouse <- {x / 2, y / 2}
        App.set_local!(App.KL.State.Local.game(local))
      else
        switch String.starts_with(event.id) {
          "SK":
            let key = String.drop(2, event.id)
            Maybe {
              get user    = game@players{local@user}
              get hero_id = user@hero_id
              get hero    = App.KL.Game.Hero.get_by_id(hero_id)
              get skill   = hero@skills{key}
              let local   = local@hud <- local@hud@skill <- some(skill)
              return App.set_local!(App.KL.State.Local.game(local))
            } <> App.set_local!(App.KL.State.Local.game(local@hud <- local@hud@skill <- none))
        } default App.set_local!(App.KL.State.Local.game(local@hud <- App.KL.Game.State.Local.hud.new(none)))
    key_down:
      case game@moment {
        preparation:
          let preview = local@preview
          switch U16.eql(event.code) {
            9#16:
              let score = case local@score {
                none: some("score")
                some: none
              }
              let local = local@score <- score
              App.set_local!(App.KL.State.Local.game(local))
            27#16:
              let preview = preview@skill <- none
              IO {
                App.new_post!(room, App.KL.Game.Phase.Play.Event.cast.cancel_casts)
                App.set_local!(App.KL.State.Local.game(local@preview <- preview))
              }
          }default
          local = 
            Maybe {
              get casted_skill  = App.KL.Game.Skill.get(local@user, event.code, game)
              let skill       =
                case preview@skill as skill {
                  none: 
                    some({event.code, 1})
                  some:
                    if skill.value@fst =? event.code then
                      none
                    else 
                      some({event.code, 1})
                }          
              return local@preview <- preview@skill <- skill
            } <> local
          IO {
            App.new_post!(room, App.KL.Game.Phase.Play.Event.cast.cancel_skill(event.code))
            App.set_local!(App.KL.State.Local.game(local))
          }
      } default App.pass!
    mouse_down: 
      case game.moment {
        initial:
          App.KL.Game.Phase.Play.when.initial.mouse_down(room, local, game)
        preparation:
          App.KL.Game.Phase.Play.when.preparation.mouse_down(room, local, game)
        execution:
          let preview = local@preview
          let preview = preview@skill <- none
          App.set_local!(App.KL.State.Local.game(local@preview <- preview))
      }
  } default App.pass!

App.KL.Game.Phase.Play.when.initial.mouse_down(room: String, local: App.KL.Game.State.Local, game: App.KL.Game): IO<Maybe<App.State.local<App.KL.State>>>
  let coord = Hexagonal.Axial.from_screen_xy(local@mouse, App.KL.Constants.hexagon_radius, U32.to_i32(local@screen_size@fst) / 4, U32.to_i32(local@screen_size@snd) / 4)
  
  if App.KL.Game.Board.Blueprint.base.valid(coord, local@user, game@players, game@blueprint) then
    App.new_post!(room, App.KL.Game.Phase.Play.Event.set_init_pos.serial(coord))
  else
    App.pass!

  

App.KL.Game.Phase.Play.when.preparation.mouse_down(room: String, local: App.KL.Game.State.Local, game: App.KL.Game): IO<Maybe<App.State.local<App.KL.State>>>
  let preview = local@preview
  case preview@skill as skill{
    none:
      let coord = Hexagonal.Axial.from_screen_xy(local@mouse, App.KL.Constants.hexagon_radius, U32.to_i32(local@screen_size@fst) / 4, U32.to_i32(local@screen_size@snd) / 4)
      let new_local = local@info <- coord
      App.set_local!(App.KL.State.Local.game(new_local))
    some:
      let {char, count} = skill.value // Skill key and count for how many coords have been chosen after selecting skill
      let coord = Hexagonal.Axial.from_screen_xy(local@mouse, App.KL.Constants.hexagon_radius, U32.to_i32(local@screen_size@fst) / 4, U32.to_i32(local@screen_size@snd) / 4)
      let skill = App.KL.Game.Skill.get(local@user, char, game)
      case skill {
        none: App.pass!
        some:
          let pattern     = App.KL.Game.Skill.get_pattern(skill.value)

          // Checks if skill can or if it will be cast at a repeating tile
          let can_cast =
            if App.KL.Game.Skill.can_repeat(skill.value) then
              true
            else
              let picks = preview@picks
              let can_cast = true 

              // Checks if there is already a pick for that tile and skill
              for new_coord:new_char in picks with can_cast: 
                can_cast && not((new_coord =? coord) && (new_char =? char))
              can_cast

          // Updates skill field in preview until all the required picks have been done
          let new_skill = 
            if count <? pattern then
              some({char, count + 1})
            else
              none
          let preview = preview@skill <- new_skill

          // Updates data
          if can_cast then
            IO {
              App.new_post!(room, App.KL.Game.Phase.Play.Event.cast.serial(char, coord))
              App.set_local!(App.KL.State.Local.game(local@preview <- preview))
            }
          else
            App.pass!
      }
    }
