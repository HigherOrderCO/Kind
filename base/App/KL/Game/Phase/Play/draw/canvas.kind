App.KL.Game.Phase.Play.draw.canvas.elem(
  local: App.KL.Game.State.Local, 
  game: App.KL.Game, img: VoxBox
): DOM
  <div style={"display": "flex", "justify-content": "center", "align-items": "center", "width": "max(100vw, 900px)", "height": "max(100vh, 650px)"}>
    <div style={
      "cursor": "url("| App.KL.Game.Phase.Play.draw.get_cursor(local, game) | "), auto"
      "max-height": "100%",
      "max-width": "100%",
      "overflow": "scroll",
      "display": "flex",
      "align-items": "center",
      "justify-content": "center"
    }>{
      let width  = F64.show(F64.round(I32.to_f64(U32.to_i32(local@screen@size@fst)) / local@screen@scale))
      let height = F64.show(F64.round(I32.to_f64(U32.to_i32(local@screen@size@snd)) / local@screen@scale))
      DOM.vbox({
          "class": "pixel-art" 
          "id": "game_screen",
          "width": width,
          "height": height,
          "scale": String.show_clean(F64.show(local@screen@scale))
        },
        {},
        App.KL.Game.Phase.Play.draw.canvas(img, local, game)) 
    }</div>
  </div>

// Auxiliar Function
App.KL.Game.Phase.Play.draw.canvas.to_xy( 
  coord: Hexagonal.Axial
  screen: App.KL.Game.Screen
): Pair<U32, U32>
    
    // Calculus to find the center of the canvas
    let x = F64.to_i32((I32.to_f64(U32.to_i32(screen@size@fst)) / screen@scale) / 2) 
    let y = F64.to_i32((I32.to_f64(U32.to_i32(screen@size@snd)) / screen@scale) / 2) 
    let {x, y} = Hexagonal.Axial.to_screen_xy(coord, App.KL.Constants.hexagon_radius, x, y)
    {x - screen@position@fst, y - screen@position@snd }


App.KL.Game.Phase.Play.draw.canvas.from_xy( 
  position: Pair<U32, U32> 
  screen: App.KL.Game.Screen
): Hexagonal.Axial

  let position = {screen@position@fst + ((2#32 * position@fst) / F64.to_u32(screen@scale)), screen@position@snd + ((2#32 * position@snd) / F64.to_u32(screen@scale))}
  let x = F64.to_i32((I32.to_f64(U32.to_i32(screen@size@fst)) / screen@scale) / 2)
  let y = F64.to_i32((I32.to_f64(U32.to_i32(screen@size@snd)) / screen@scale) / 2)
  Hexagonal.Axial.from_screen_xy(position, App.KL.Constants.hexagon_radius, x, y)
    

// CANVAS DRAWING
// ================

// draw canvas
App.KL.Game.Phase.Play.draw.canvas(
  img: VoxBox
  local: App.KL.Game.State.Local
  game: App.KL.Game
): VoxBox
  open game
  let hits = case game.moment {
    initial: none
    preparation: none
    execution: some(game.moment.hits)
  }
  let img = App.KL.Game.Phase.Play.draw.canvas.board(game, local@preview, hits, local@user, local@mouse, local@screen, img) 
  let img = App.KL.Game.Phase.Play.draw.canvas.bases(game, local@user, local@screen, img)
  let img = App.KL.Game.Phase.Play.draw.canvas.cursor(local@mouse, game.board, local@screen, img)
  img


// draw canvas board
App.KL.Game.Phase.Play.draw.canvas.landscape(
  blueprint: App.KL.Game.Board.Blueprint,
  screen: App.KL.Game.Screen
  img: VoxBox
): VoxBox
  let landscape = blueprint@landscape
  for coord:tile in landscape with img:
    App.KL.Game.Phase.Play.draw.canvas.tile.terrain(tile, Hexagonal.Axial.Map.new!, none, coord, screen, img)
  img


App.KL.Game.Phase.Play.draw.canvas.board(
  game:    App.KL.Game
  preview: App.KL.Game.Cast.Preview
  hits:    Maybe<App.KL.Game.Indicators>
  user:    String
  mouse:   Pair<U32, U32>
  screen:  App.KL.Game.Screen
  img:     VoxBox
): VoxBox
  open game
  let team        = App.KL.Game.Phase.Draft.to_team(game@players, user) <> App.KL.Game.Team.neutral
  let map         = game@board
  let mouse_coord = App.KL.Game.Phase.Play.draw.canvas.from_xy(mouse, screen)
  let indicators  = App.KL.Game.Phase.Play.Draw.canvas.get_indicators(preview, mouse_coord, user, game)
  let ap_used     = App.KL.Game.Moment.get_player_used_ap(game) // If there is a skill being cast, returns the player address and used ap
  let img         = App.KL.Game.Phase.Play.draw.canvas.landscape(game@blueprint, screen, img)

  for coord:tile in map with img:
    let indicator = Hexagonal.Axial.Map.get!(coord, hits <> indicators)
    let img = App.KL.Game.Phase.Play.draw.canvas.grid(coord, game@moment, screen, img)
    let img = App.KL.Game.Phase.Play.draw.canvas.tile.terrain(tile@terrain, preview@picks, indicator, coord, screen, img)
    let img = App.KL.Game.Phase.Play.draw.canvas.tile.indicator(indicator, coord, screen, img)
    let img = App.KL.Game.Phase.Play.draw.canvas.tile.creature(game, tile@creature, coord, ap_used, screen, user, img)
    let img = App.KL.Game.Phase.Play.draw.canvas.tile.token(game, tile@token, coord, screen, team, img)
    img
  
  let img = App.KL.Game.Phase.Play.draw.canvas.initial(user, game@players, game@blueprint, screen, game@moment, img)
  img

// TODO optimize?
App.KL.Game.Phase.Play.draw.canvas.grid(
  coord:  Hexagonal.Axial
  moment: App.KL.Game.Moment
  screen: App.KL.Game.Screen
  img:    VoxBox
): VoxBox
  case moment {
    initial: img
    preparation:
      let indicator     = App.KL.Game.Indicator.gray_line
      let indicator_img = App.KL.Game.Indicator.get_img(indicator)
      
      let {x, y} = App.KL.Game.Phase.Play.draw.centralize.new(coord, screen)
      VoxBox.Draw.image(x, y, App.KL.Constants.z_index.grid, indicator_img, img)
    execution: img
  }

App.KL.Game.Phase.Play.draw.canvas.tile.token(
  game: App.KL.Game
  token: Maybe<App.KL.Game.Token>,
  token_coord: Hexagonal.Axial,
  screen: App.KL.Game.Screen
  team: App.KL.Game.Team
  img: VoxBox
): VoxBox
  without token: img
  let col = 
    case App.KL.Game.Token.get_dominance(token) as team {
      none: Col32.new(100,100,100,255)
      some:
        if App.KL.Game.Team.eql(team, team.value) then
          Col32.new(0, 0, 255, 255)
        else
          if App.KL.Game.Team.eql(App.KL.Game.Team.neutral, team.value) then
            Col32.new(100,100,100,255)
          else
            Col32.new(255, 0, 0, 255)
    }

  let coords = Hexagonal.Axial.range(token_coord, token@range)
  let board = game@board
  for coord in coords with img:
    let tile = Hexagonal.Axial.Map.get!(coord, board)
    case tile {
      none:
        img
      some:
        let {cx, cy} = App.KL.Game.Phase.Play.draw.canvas.to_xy(coord, screen)
        if Hexagonal.Axial.eql(coord, token_coord) then
          VoxBox.Draw.square(cx,cy,App.KL.Constants.z_index.token,10,10,col,img)
        else
          VoxBox.Draw.square(cx,cy,App.KL.Constants.z_index.token,5,5,col,img)
    }
  img


// draw initial terrains in canvas board
App.KL.Game.Phase.Play.draw.canvas.initial(
  user_id: String
  players: Map<App.KL.Game.Player>
  blueprint: App.KL.Game.Board.Blueprint
  screen: App.KL.Game.Screen
  moment: App.KL.Game.Moment
  img: VoxBox
): VoxBox
  case moment {
    initial:
      let initial_coords = blueprint@initial
      for coord:terrain_team in initial_coords with img:
        // get terrain according with local player team
        let options = 
          {App.KL.Game.Terrain.new(5, none), App.KL.Game.Terrain.new(7, none)} // blue and red
        let terrain = 
          App.KL.Game.Phase.Play.draw.canvas.initial.choose_by_team!(user_id, players, terrain_team, options)
        // draw terrain
        App.KL.Game.Phase.Play.draw.canvas.tile.terrain(terrain, Hexagonal.Axial.Map.new!, none, coord, screen, img)
      // return img
      img
    preparation: img
    execution: img
  }

// function to choose between two options based on player team
// if team is not equal to pivot_team second option is choose
// otherwise first option is choose (equal and neutral cases)
App.KL.Game.Phase.Play.draw.canvas.initial.choose_by_team<A: Type>(
  user_id: String
  players: Map<App.KL.Game.Player>
  pivot_team: App.KL.Game.Team
  options: Pair<A, A>
): A
  // get player to know which terrain draw
  let player = players{user_id}
  // set player team; if no player then neutral is assigned
  let player_team = Maybe.map!!((x) x@team, player) <> App.KL.Game.Team.neutral

  // if team is neutral all terrains will be blue
  if player_team =? App.KL.Game.Team.neutral then
    options@fst
  else
    // if player team is equal to the team of the terrain draw blue
    if player_team =? pivot_team then
      options@fst
    // else draw red
    else
      options@snd

App.KL.Game.Phase.Play.draw.canvas.bases(
  game: App.KL.Game
  user_id: String
  screen: App.KL.Game.Screen
  img: VoxBox
): VoxBox
  case game@moment {
    initial:
      let players      = game@players
      let local_player = players{user_id}
      without local_player: img

      let bases  = game@blueprint@bases
      for coord:indicator_team in bases with img:
        let options   = {App.KL.Game.Indicator.blue_line, App.KL.Game.Indicator.red_line}
        let indicator = 
          App.KL.Game.Phase.Play.draw.canvas.initial.choose_by_team!(user_id, players, indicator_team, options)
        App.KL.Game.Phase.Play.draw.canvas.tile.indicator(some(indicator), coord, screen, img)

      let allies = App.KL.Game.Player.get_allies(local_player, players)
      for id:player in allies with img:
        let init_pos = player@init_pos
        without init_pos: img
        let indicator =
          if String.eql(user_id, id) then 
            App.KL.Game.Indicator.green_line
          else
            App.KL.Game.Indicator.gray_line
        App.KL.Game.Phase.Play.draw.canvas.tile.indicator(some(indicator), init_pos, screen, img)
      img
    preparation: img
    execution: img
  }

App.KL.Game.Phase.Play.Draw.canvas.get_indicators(
  preview: App.KL.Game.Cast.Preview 
  target: Hexagonal.Axial,
  user:   String,
  game:   App.KL.Game
): App.KL.Game.Indicators

  Maybe {
    get pair       = preview@skill
    let skill_key  = pair@fst
    get skill      = App.KL.Game.Skill.get(user, skill_key, game)
    let targets    = App.KL.Game.Picks.get_skill_coords(skill_key, preview@picks)
    let targets    = target & targets
    let game       = App.KL.Game.Cast.simulate(skill@delay, (x) String.eql(x@player, user), game)
    get center     = App.KL.Game.Board.find_player_coord(user, game@board)
    get creature   = App.KL.Game.Board.Creature.get(center, game@board)
    let indicator  = App.KL.Game.Indicator.blue_line
    let indicators = Hexagonal.Axial.Map.new!
    //let distance   = App.KL.Game.Creature.Status.movement_range(Nat.to_i32(U64.to_nat(skill@range)), creature)
    let coords     = skill@range(game@board, center, targets)
    // set range indicator
    for coord in coords with indicators:
      Hexagonal.Axial.Map.set!(coord, indicator, indicators)
    let areas = App.KL.Game.Effect.indicators.get_indicators(center, skill, {skill_key, user} targets, game@board)
    return Hexagonal.Axial.Map.union!(indicators, areas)
  } <> Hexagonal.Axial.Map.new<App.KL.Game.Indicator>

// draw mouse indicator
App.KL.Game.Phase.Play.draw.canvas.cursor(
  mouse: Pair<U32, U32>
  board: App.KL.Game.Board
  screen: App.KL.Game.Screen
  img: VoxBox
): VoxBox
  let coord = App.KL.Game.Phase.Play.draw.canvas.from_xy(mouse, screen)
  let {x,y} = App.KL.Game.Phase.Play.draw.centralize.old(coord, screen)
  let has_tile = App.KL.Game.Tile.get(coord, board)
  case has_tile { 
    none:
      img
    some:
      VoxBox.Draw.image(x, y, App.KL.Constants.z_index.cursor, App.KL.Game.Field.Mouse.mouse_ui, img)
  }

// draws tile terrain
App.KL.Game.Phase.Play.draw.canvas.tile.terrain(
  terrain:    App.KL.Game.Terrain
  picks:      App.KL.Game.Picks
  indicator:  Maybe<App.KL.Game.Indicator>
  tile_coord: Hexagonal.Axial
  screen: App.KL.Game.Screen
  img:        VoxBox
): VoxBox
  let indicator = indicator <> App.KL.Game.Indicator.background
  let {i, j}    = App.KL.Game.Phase.Play.draw.centralize.new(tile_coord, screen)
  let field     = App.KL.Game.Field.get_by_id.default(terrain@field_id)
  let field_drawing = field@draw(terrain, indicator)
  let tile_drawing  = VoxBox.Draw.image(i, j, I32.to_u32(tile_coord@j + App.KL.Constants.z_index.terrain), field_drawing, img)
  let tile_drawing  = App.KL.Game.Phase.Play.draw.letter(tile_coord, picks, screen, tile_drawing)
  tile_drawing

App.KL.Game.Phase.Play.draw.canvas.tile.indicator(
  indicator:  Maybe<App.KL.Game.Indicator>
  tile_coord: Hexagonal.Axial
  screen:     App.KL.Game.Screen
  img:        VoxBox
): VoxBox
  without indicator: img
    let {i, j} = App.KL.Game.Phase.Play.draw.centralize.new(tile_coord, screen)
    let ind_img = App.KL.Game.Indicator.get_img(indicator)
    let img = VoxBox.Draw.image(i, j, App.KL.Constants.z_index.indicator, ind_img, img)
    img

App.KL.Game.Phase.Play.draw.canvas.tile.creature.get_draw_pose(game: App.KL.Game, coord: Hexagonal.Axial): App.KL.Game.Hero.Draw.Pose
  open game
  case game.moment {
    initial: App.KL.Game.Hero.Draw.Pose.idle
    preparation:
      App.KL.Game.Hero.Draw.Pose.idle
    execution: case game.moment.casts {
      nil:
        App.KL.Game.Hero.Draw.Pose.idle
      cons: Maybe { 
        //get success = if game.moment.success then some(unit) else none
        get success = case game.moment.success { 
          left: none
          right: some(unit)
          }
        get creature = App.KL.Game.Board.Creature.get(coord, game.board)
        let cast = game.moment.casts.head
        get player = creature@player
        open cast
        if String.eql(player, cast.player) then Maybe {
          let frame = game.moment.frame
          let center = game.moment.coord <> coord
          let target = cast.target
          let letter = cast.letter
          get skill = App.KL.Game.Cast.get_skill(cast, game)
          get creature = App.KL.Game.Cast.get_creature(cast, game)
          return App.KL.Game.Hero.Draw.Pose.cast(frame, center, target, letter, skill, creature)
        } else none
      } <> App.KL.Game.Hero.Draw.Pose.idle
    }
  }

// draws tile creature if any
App.KL.Game.Phase.Play.draw.canvas.tile.creature(
  game: App.KL.Game
  creature: Maybe<App.KL.Game.Creature>,
  coord: Hexagonal.Axial,
  ap_used: Maybe<Pair<String, I32>>
  screen: App.KL.Game.Screen
  user_id: String
  img: VoxBox
): VoxBox
  case creature {
    none: img
    some:
      let team = App.KL.Game.Phase.Draft.to_team(game@players, user_id)
      let draw = App.KL.Game.Board.Creature.get.draw(creature.value)
      let draw_pose = App.KL.Game.Phase.Play.draw.canvas.tile.creature.get_draw_pose(game, coord)
      let {draw_coord, draw_voxbox} = draw@vbox_img(draw_pose)
      let draw_coord = draw_coord <> coord
      //let aux = I32.to_u32(App.KL.Constants.hexagon_radius) //U32
      //let cy = cy - (aux * 2) //U32
      //let cx = cx - aux //U32
      //log(U32.show( cy + 100))
      let {cx, cy} = App.KL.Game.Phase.Play.draw.canvas.to_xy(draw_coord, screen)
      let img_x = cx - (128::U32)
      let img_y = cy - (128::U32)
      let height = I32.to_u32(draw@height)
      let img = VoxBox.Draw.image(img_x, img_y, App.KL.Constants.z_index.hero, draw_voxbox, img)
      open creature.value as creature
      case creature.player { 
        none:
          img
        some:
          let img = App.KL.Game.Phase.Play.Bar.show_hp(cx - 17, (cy - height), user_id, team <> App.KL.Game.Team.neutral, creature.value, img)
          let img = App.KL.Game.Phase.Play.draw.ap(img_x - 12, (img_y - height) - 3, creature.value, ap_used, img)
          img
      }

  }
