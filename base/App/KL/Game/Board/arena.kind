// The 'Arena' board
App.KL.Game.Board.arena: App.KL.Game.Board
  extra_width  = App.KL.Constants.board_extra_width
    board_size = App.KL.Constants.board_size
  n_board_size = I32.neg(board_size)

  board  = Hexagonal.Axial.Map.new<App.KL.Game.Tile>
  
  new_terrain = App.KL.Game.Terrain.new(0,none)
  new_terrain = App.KL.Game.Entity.terrain(new_terrain)

  tokens = [
    { {0, 0} :: Hexagonal.Axial, App.KL.Game.Token.new(0, 1, 1, 2) }
    { {0,-5} :: Hexagonal.Axial, App.KL.Game.Token.new(0, 1, 1, 2) }
    { {0, 5} :: Hexagonal.Axial, App.KL.Game.Token.new(0, 1, 1, 2) }
  ]
    
  for j : I32 from n_board_size to (board_size + 1) with board:
    // https://www.redblobgames.com/grids/hexagons/#range-coordinate
    // iterates only in valid coordinates
    let a = I32.max(n_board_size, I32.neg(j) - board_size)
    let b = I32.min(  board_size, I32.neg(j) + board_size)

    for i : I32 from (a - extra_width) to (b + extra_width + 1) with board: 
      // generate int axial coordinate
      coord   = Hexagonal.Axial.new(i, j)
      App.KL.Game.Board.push(coord, new_terrain, board)
      
    board

  for token in tokens with board:
    let token_coord = token@fst
    let token = token@snd
    let entity = App.KL.Game.Entity.token(token)
    App.KL.Game.Board.push(token_coord, entity, board)

  board

