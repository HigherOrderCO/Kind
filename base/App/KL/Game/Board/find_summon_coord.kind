// TODO: optimize
// Returns the position of a player
App.KL.Game.Board.find_summon_coord(
  summon: String,
  board: App.KL.Game.Board
): Maybe<Hexagonal.Axial>
  let list = Hexagonal.Axial.Map.to_list!(board)
  App.KL.Game.Board.find_summon_coord.go(summon, list)

App.KL.Game.Board.find_summon_coord.go(
  summon: String,
  board: List(Pair(Hexagonal.Axial,App.KL.Game.Tile))
): Maybe<Hexagonal.Axial>
  case board {
    nil: none
    cons:
      let go = App.KL.Game.Board.find_summon_coord.go(summon, board.tail)
      let tile = board.head@snd
      let creature = tile@creature
      without creature: go
      case creature@category as category {
        hero:
          go
        summon:
          let creature_player = creature@player 
          without creature_player: go
          if creature_player =? summon then
            some(board.head@fst)
          else
            go
      }
  }
