type App.GG.Tile {
  new(
    // img: VoxBox
    projectiles: List<App.GG.Projectile>
    creature: Maybe<App.GG.Creature>
    owner: App.GG.Team
    statuses: App.GG.Tile.Statuses
  )
}

type App.GG.Tile.Statuses {
  new(
    stolen: Maybe<App.GG.Tile.Status>
    broken: Maybe<App.GG.Tile.Status>
    reserved: Maybe<App.GG.Tile.Status>
  )
}

App.GG.Tile.Statuses.pure: App.GG.Tile.Statuses
  App.GG.Tile.Statuses.new(none, none, none)

type App.GG.Tile.Status {
  stolen(duration: U64)
  broken(duration: U64)
  reserved(duration: U64, id: Nat)
}

App.GG.Tile.update_creature(
  update: App.GG.Creature -> App.GG.Creature, 
  tile: App.GG.Tile
): App.GG.Tile
  case tile@creature as creature{
    none: tile
    some: 
      let new_creature = update(creature.value)
      let new_tile = tile@creature <- some(new_creature)
      new_tile
  }

App.GG.Tile.original_owner(coord: App.GG.Coord): App.GG.Team
  if coord@fst <? 4 then
    App.GG.Team.blue
  else if coord@fst <? 8 then 
    App.GG.Team.red
  else
    App.GG.Team.neutral

App.GG.Tile.original_owner.update(coord: App.GG.Coord, tile: App.GG.Tile): App.GG.Tile
  let new_team = App.GG.Tile.original_owner(coord)
  tile@owner <- new_team

App.GG.Tile.Status.del_from_nat(count: Nat, tile: App.GG.Tile): App.GG.Tile
  let statuses = tile@statuses
  let new_statuses = 
    switch Nat.eql(count) {
    0: statuses@stolen <- none
    1: statuses@broken <- none
    2: statuses@reserved <- none
  }default statuses
  tile@statuses <- new_statuses

App.GG.Tile.Status.add(status: App.GG.Tile.Status, tile: App.GG.Tile): App.GG.Tile
  let statuses = tile@statuses
  let new_statuses = 
    case status {
      stolen: statuses@stolen <- some(status)
      broken: statuses@broken <- some(status)
      reserved: statuses@reserved <- some(status)
    }
  tile@statuses <- new_statuses

App.GG.Tile.Status.get_from_nat(count: Nat, statuses: App.GG.Tile.Statuses): Maybe<Maybe<App.GG.Tile.Status>>
  switch Nat.eql(count) {
    0: some(statuses@stolen)
    1: some(statuses@broken)
    2: some(statuses@reserved)
  }default none

App.GG.Tile.Status.update_grid(coord: App.GG.Coord, game: App.GG.Game): App.GG.Game
  App.GG.Tile.Status.update_grid.go(0, coord, game)

App.GG.Tile.Status.update_grid.go(count: Nat, coord: App.GG.Coord, game: App.GG.Game): App.GG.Game
  let new_local = 
    Maybe {
      get tile = App.GG.Grid.get(coord, game@grid)
      let statuses = tile@statuses
      get status = App.GG.Tile.Status.get_from_nat(count, statuses)
      let new_local = 
        case status {
          none: game 
          some: 
            case status.value {
              stolen: App.GG.Tile.Status.update_stolen(status.value, coord, game)
              broken: App.GG.Tile.Status.update_broken(status.value, coord, game)
              reserved: App.GG.Tile.Status.update_reserved(status.value, coord, game)
            }default game
        }
      let new_local = App.GG.Tile.Status.update_grid.go(count +1, coord, new_local)
      return new_local
    } <> game
  new_local

//App.GG.Tile.Status.update(status: App.GG.Tile.Status, coord: App.GG.Coord, grid: App.GG.Grid): App.GG.Grid

App.GG.Tile.Status.update_stolen(status: App.GG.Tile.Status, coord: App.GG.Coord, game: App.GG.Game): App.GG.Game
  case status {
    stolen: 
      let grid = game@grid
      //log(U64.show(status.duration))
      if status.duration =? 0 then
        let can_rtrn = App.GG.Tile.can_be_returned(coord, grid)
        if can_rtrn then 
          let new_tile = App.GG.Tile.original_owner.update(coord)
          let new_grid = App.GG.Grid.update(coord, new_tile, grid)
          let new_tile = App.GG.Tile.Status.del_from_nat(0)
          let new_grid = App.GG.Grid.update(coord, new_tile, new_grid)
          let new_local = game@grid <- new_grid
          new_local
        else
          game
      else
        let new_tile = App.GG.Tile.Status.add(App.GG.Tile.Status.stolen(status.duration - 1))
        let new_grid = App.GG.Grid.update(coord, new_tile, grid)
        let new_local = game@grid <- new_grid 
        new_local
  }default game

App.GG.Tile.Status.update_broken(status: App.GG.Tile.Status, coord: App.GG.Coord, game: App.GG.Game): App.GG.Game
  let grid = game@grid
  case status {
    broken: 
      if status.duration =? 0 then
        let new_tile = App.GG.Tile.Status.del_from_nat(1)
        let new_grid = App.GG.Grid.update(coord, new_tile, grid)
        let new_local = game@grid <- new_grid
        new_local
      else
        let new_tile = App.GG.Tile.Status.add(App.GG.Tile.Status.broken(status.duration - 1))
        let new_grid = App.GG.Grid.update(coord, new_tile, grid)
        let new_local = game@grid <- new_grid
        new_local
  }default game
// New Status

App.GG.Tile.Status.update_reserved(status: App.GG.Tile.Status, coord: App.GG.Coord, game: App.GG.Game): App.GG.Game
  let grid = game@grid
  case status {
    reserved: 
      if status.duration =? 0 then
        let new_tile = App.GG.Tile.Status.del_from_nat(2)
        let new_grid = App.GG.Grid.update(coord, new_tile, grid)
        let new_local = game@grid <- new_grid
        new_local
      else
        let new_tile = App.GG.Tile.Status.add(App.GG.Tile.Status.reserved(status.duration - 1, status.id))
        let new_grid = App.GG.Grid.update(coord, new_tile, grid)
        let new_local = game@grid <- new_grid
        new_local
  }default game

App.GG.Tile.can_be_returned(coord: App.GG.Coord, grid: App.GG.Grid): Bool
  let coords = App.GG.Coord.self_and_front_columns(coord, grid)
  let tile = App.GG.Grid.get(coord, grid)
  without tile: false
  let team = tile@owner
  let can_return = true
  for c in coords with can_return:
    let creature = App.GG.Creature.get(c, grid)
    without creature: can_return
    if App.GG.Team.eql(tile@owner, creature@team) then 
      false
    else
      can_return
  can_return
