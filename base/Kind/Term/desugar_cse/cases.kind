Kind.Term.desugar_cse.cases(
  expr: Kind.Term,
  name: Kind.Name,
  wyth: List(Kind.Def),
  cses: BitsMap(Kind.Term),
  type: Kind.Term,
  defs: Kind.Defs,
  ctxt: Kind.Context,
): Kind.Term
  case Kind.Term.reduce(type, defs) as type {
    all: 
      let got = Maybe.or!(Kind.Map.get!(type.name, cses), Kind.Map.get!("_", cses));
      case got {
        some:
          let argm = Kind.Term.desugar_cse.argument(name, wyth, type.xtyp, got.value, defs);
          let expr = Kind.Term.app(expr, argm);
          let type = type.body(Kind.Term.var(type.self,0), Kind.Term.var(type.name,0));
          Kind.Term.desugar_cse.cases(expr, name, wyth, cses, type, defs, ctxt),
        none: 
          let expr = for defn in wyth:
            open defn
            Kind.Term.app(expr, defn.term)
          expr
      },
    _:
      let expr = for defn in wyth:
        Kind.Term.app(expr, case defn { new: defn.term })
      expr
  }
