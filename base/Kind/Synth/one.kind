Kind.Synth.one(name: Kind.Name, defs: Kind.Defs): IO(Maybe(Kind.Defs))
  //log("synth ", name)
  case Kind.get!(name, defs) as got {
    none: IO {
      var loaded = Kind.Synth.load(name, defs);
      case loaded {
        none: IO {
          return none;
        },
        some: IO {
          Kind.Synth.one(name, loaded.value);
        },
      };
    },
    some: case got.value {
      new: IO {
        let file = got.value.file;
        let code = got.value.code;
        let orig = got.value.orig;
        let name = got.value.name;
        let term = got.value.term;
        let type = got.value.type;
        let isct = got.value.isct;
        let arit = got.value.arit;
        let stat = got.value.stat;
        //let skip = Debug.log!(String.flatten([name, ": ", Kind.Term.show(type), " = ", Kind.Term.show(term)]), (x) Unit.new);
        //Debug.log!(String.flatten(["synth ", name, " ", case stat { init: "INIT", wait: "WAIT", done: "DONE", fail: "FAIL" }]), (x)
        case stat {
          wait: IO { return some(defs); },
          done: IO { return some(defs); },
          fail: IO { return some(defs); },
          init: IO {
            let defs = Kind.set!(name, Kind.Def.new(file, code, orig, name, term, type, isct, arit, Kind.Status.wait), defs);
            let checked = do Kind.Check {
              var chk_type = Kind.Term.check(type, Maybe.some!(Kind.Term.typ), defs, [], Kind.MPath.i(Kind.MPath.nil), Maybe.none!);
              var chk_term = Kind.Term.check(term, Maybe.some!(type), defs, [], Kind.MPath.o(Kind.MPath.nil), Maybe.none!);
              return Unit.new;
            };
            case checked {
              result:
                //let skip = Debug.log!(String.join("\n", List.mapped!(checked.errors)!((x) String.concat("-- ", Kind.Error.show(x,Map.new!)))), (x) Unit.new);
                if List.is_empty!(checked.errors) then IO {
                  let defs = Kind.define(file, code, orig, name, term, type, isct, arit, true, defs);
                  //let defs = Kind.set!(name, Kind.Def.new(file, code, name, term, type, Kind.Status.done), defs);
                  return some(defs);
                } else IO {
                  var fixed = Kind.Synth.fix(file, code, orig, name, term, type, isct, arit, defs, checked.errors, Bool.false);
                  case fixed {
                    none: IO {
                      let stat = Kind.Status.fail(checked.errors);
                      let defs = Kind.set!(name, Kind.Def.new(file, code, orig, name, term, type, isct, arit, stat), defs);
                      return some(defs);
                    },
                    some: Kind.Synth.one(name, fixed.value),
                  };
                }
            };
          }
        };
      }
    }
  }
