Kind.Core.read.is_name(chr: Char): Bool
  Bool.or(Bool.and(U16.gte(chr,46#16), U16.lte(chr,47#16)),  // ./
  Bool.or(Bool.and(U16.gte(chr,48#16), U16.ltn(chr,58#16)),  // 0-9
  Bool.or(Bool.and(U16.gte(chr,65#16), U16.ltn(chr,91#16)),  // A-Z
  Bool.or(Bool.and(U16.gte(chr,95#16), U16.ltn(chr,96#16)),  // _
  Bool.or(Bool.and(U16.gte(chr,97#16), U16.ltn(chr,123#16)), // a-z
  Bool.false)))))

Kind.Core.read.name(code: String): Pair<String, String>
  case code {
    nil:
      {code, ""}
    cons:
      if Kind.Core.read.is_name(code.head) then
        let {code, name} = Kind.Core.read.name(code.tail)
        {code, String.cons(code.head, name)}
      else
        {code, ""}
  }
  
Kind.Core.read.spaces(code: String): Pair<String, Unit>
  case code {
    nil:
      {code, unit}
    cons:
      let is_space =
        Bool.or(U16.eql(code.head, ' '),
        Bool.or(U16.eql(code.head, '\n'),
        Bool.or(U16.eql(code.head, '\r'),
        Bool.or(U16.eql(code.head, '\t'),
        Bool.or(U16.eql(code.head, '\v'),
        Bool.or(U16.eql(code.head, '\f'),
        Bool.false))))))
      if is_space then
        Kind.Core.read.spaces(code.tail)
      else
        {code, unit}
  }

Kind.Core.read.char(code: String, chr: Char): Pair<String, Unit>
  let {code, skip} = Kind.Core.read.spaces(code)
  case code {
    nil:
      {"", unit} // unexpected EOF
    cons:
      if U16.eql(code.head, chr) then
        {code.tail, unit}
      else
        {"", unit} // unexpected character
  }

Kind.Core.read.u16(code: String, u16: Char): Pair<String, Char>
  case code {
    nil:
      {code, u16}
    cons:
      if Char.is_hex(code.head) then
        let u16 = U16.add(U16.mul(u16,16#16), Char.hex_value16(code.head))
        Kind.Core.read.u16(code.tail, u16)
      else
        {code, u16}
  }

Kind.Core.read.natx(code: String, nat: Nat): Pair<String, Nat>
  case code {
    nil:
      {code, nat}
    cons:
      if Char.is_hex(code.head) then
        let nat = Nat.add(Nat.mul(nat,10), Char.dec_value(code.head))
        Kind.Core.read.natx(code.tail, nat)
      else
        {code, nat}
  }

Kind.Core.read.chrx(code: String): Pair<String, Char>
  if String.starts_with(code, "\\u{") then
    let code = String.drop(3, code)
    let {code, chrx} = Kind.Core.read.u16(code, 0#16)
    if String.starts_with(code, "}") then
      {String.drop(1, code), chrx}
    else
      {"", '?'}
  else if String.starts_with(code, "\\'") then
    {String.drop(2,code), '\''}
  else if String.starts_with(code, "\\\"") then
    {String.drop(2,code), '\"'}
  else if String.starts_with(code, "\\\\") then
    {String.drop(2,code), '\\'}
  else case code {
    nil: {"", '?'}
    cons: {code.tail, code.head}
  }
        
Kind.Core.read.strx(code: String): Pair<String, String>
  if String.starts_with(code, "\"") then
    {code, ""}
  else
    let {code, chrx} = Kind.Core.read.chrx(code)
    let {code, rest} = Kind.Core.read.strx(code)
    {code, String.cons(chrx, rest)}

Kind.Core.read.find<A: Type>(list: List<A>, cond: A -> Nat -> Bool, indx: Nat, skip: Nat): Maybe<Pair<A,Nat>>
  case list {
    nil:
      none
    cons:
      if cond(list.head, indx) then
        if Nat.eql(skip, 0) then
          some({list.head, indx})
        else
          Kind.Core.read.find!(list.tail, cond, Nat.succ(indx), Nat.pred(skip))
      else
        Kind.Core.read.find!(list.tail, cond, Nat.succ(indx), skip)
  }

Kind.Core.read.term(code: String): Pair<String, List<Pair<String,Kind.Term>> -> Kind.Term>
  let {code, skip} = Kind.Core.read.spaces(code)
  case code {
    nil:
      {"", (ctx) Kind.Term.ref("error")}
    cons:
      let head = code.head
      let code = code.tail
      if Char.eql(head, '*') then
        {code, (ctx) Kind.Term.typ}
      else if U16.eql(head, '@') then
        let {code, self} = Kind.Core.read.name(code)
        let {code, skip} = Kind.Core.read.char(code, '(')
        let {code, name} = Kind.Core.read.name(code)
        let {code, skip} = Kind.Core.read.char(code, ':')
        let {code, bind} = Kind.Core.read.term(code)
        let {code, skip} = Kind.Core.read.char(code, ')')
        let {code, body} = Kind.Core.read.term(code)
        {code, (ctx) Kind.Term.all(false, self, name, bind(ctx), (s,x) body({name,x}&{self,s}&ctx))}
      else if U16.eql(head, '%') then
        let {code, self} = Kind.Core.read.name(code)
        let {code, skip} = Kind.Core.read.char(code, '(')
        let {code, name} = Kind.Core.read.name(code)
        let {code, skip} = Kind.Core.read.char(code, ':')
        let {code, bind} = Kind.Core.read.term(code)
        let {code, skip} = Kind.Core.read.char(code, ')')
        let {code, body} = Kind.Core.read.term(code)
        {code, (ctx) Kind.Term.all(true, self, name, bind(ctx), (s,x) body({name,x}&{self,s}&ctx))}
      else if U16.eql(head, '#') then
        let {code, name} = Kind.Core.read.name(code)
        let {code, body} = Kind.Core.read.term(code)
        {code, (ctx) Kind.Term.lam(name, (x) body({name,x} & ctx))}
      else if U16.eql(head, '(') then
        let {code, func} = Kind.Core.read.term(code)
        let {code, argm} = Kind.Core.read.term(code)
        let {code, skip} = Kind.Core.read.char(code, ')')
        {code, (ctx) Kind.Term.app(func(ctx), argm(ctx))}
      else if U16.eql(head, '!') then
        let {code, name} = Kind.Core.read.name(code)
        let {code, skip} = Kind.Core.read.char(code, '=')
        let {code, expr} = Kind.Core.read.term(code)
        let {code, skip} = Kind.Core.read.char(code, ';')
        let {code, body} = Kind.Core.read.term(code)
        {code, (ctx) Kind.Term.let(name, expr(ctx), (x) body({name,x} & ctx))}
      else if U16.eql(head, '$') then
        let {code, name} = Kind.Core.read.name(code)
        let {code, skip} = Kind.Core.read.char(code, '=')
        let {code, expr} = Kind.Core.read.term(code)
        let {code, skip} = Kind.Core.read.char(code, ';')
        let {code, body} = Kind.Core.read.term(code)
        {code, (ctx) Kind.Term.def(name, expr(ctx), (x) body({name,x} & ctx))}
      else if U16.eql(head, '{') then
        let {code, expr} = Kind.Core.read.term(code)
        let {code, skip} = Kind.Core.read.char(code, ':')
        let {code, type} = Kind.Core.read.term(code)
        let {code, skip} = Kind.Core.read.char(code, '}')
        {code, (ctx) Kind.Term.ann(false, expr(ctx), type(ctx))}
      else if U16.eql(head, '\'') then
        let {code, chrx} = Kind.Core.read.chrx(code)
        let {code, skip} = Kind.Core.read.char(code, '\'')
        {code, (ctx) Kind.Term.chr(chrx)}
      else if U16.eql(head, '"') then
        let {code, strx} = Kind.Core.read.strx(code)
        let {code, skip} = Kind.Core.read.char(code, '"')
        {code, (ctx) Kind.Term.str(strx)}
      else if U16.eql(head, '+') then
        let {code, natx} = Kind.Core.read.natx(code, 0)
        {code, (ctx) Kind.Term.nat(natx)}
      else
        if Kind.Core.read.is_name(head) then
          let {code, name} = Kind.Core.read.name(code)
          let name = String.cons(head, name)
          let {code, brui} = case code {
            nil: {"", 0}
            cons: if U16.eql(code.head,'^')
              then Kind.Core.read.natx(code.tail, 0)
              else {code, 0}
          }
          {code, (ctx)
            def fun = (x,i) String.eql(Pair.fst!!(x), name)
            let got = Kind.Core.read.find!(ctx, fun, 0, brui)
            case got {
              some: Pair.snd!!(Pair.fst!!(got.value))
              none: Kind.Term.ref(name)
            }}
        else
          {"", (ctx) Kind.Term.ref("error")}
  }

Kind.Core.read(code: String): Maybe<Kind.Term>
  let {code, term} = Kind.Core.read.term(code | ";")
  case code {
    nil: none
    cons: some(term([]))
  }
