// Bitcons: a decentralized linked list (aka Bitcoin without the coin)
// This file is just an early draft.

// The body of a Bitcons Post can hold up to 8 words
Bitcons.Body: Type
  Vector<U256,40>

// A Bitcons hash is a 256-bit word
Bitcons.Hash: Type
  U256

// An IP networking address
type Bitcons.Address {
  ipv4(ip0: U8, ip1: U8, ip2: U8, ip3: U8, port: U16)
} deriving (serializer, deserializer)

// A networking peer
type Bitcons.Peer {
  new(
    last_seen: Nat
    address: Bitcons.Address
  )
}

type Bitcons.Message {
  ping(addresses: List<Bitcons.Address>)
  request_post(hash: Bitcons.Hash)
  share_post(post: Bitcons.Post)
} deriving (serializer, deserializer)

// A network message received from a sender
type Bitcons.Received {
  new(
    sender: Bitcons.Peer
    message: Bitcons.Message
  )
}

// A Bitcons post
type Bitcons.Post {
  new(
    body: Bitcons.Body // post contents (1280 bytes)
    work: U256         // extra info and nonce (32 bytes)
    prev: U256         // previous post (32 bytes)
  )
} deriving (serializer, deserializer)

// Holds all the posts and aux structures
type Bitcons.Store {
  new(
    post: Map<Bitcons.Post>           // maps PostHash to Post object
    seen: Map<Unit>                   // set of PostHash that I have downloaded
    children: Map<List<Bitcons.Hash>> // maps PostHash to list of PostHash that link to it
    pending: Map<List<Bitcons.Post>>  // maps PostHash to list of posts waiting it for inclusion
    score: Map<Bitcons.Hash>          // maps PostHash to cached score
    tip: Pair<U256,Bitcons.Hash>      // current tip PostHash
  )
}

// The state of a Bitcons node
type Bitcons.Node {
  new(
    port: Nat
    peers: Map<Bitcons.Peer>
    store: Bitcons.Store
  )
}

Bitcons.serialize.uint(size: Nat, value: Nat): Bits
  case size {
    zero: Bits.e
    succ: if (value % 2) =? 0
      then Bits.o(Bitcons.serialize.uint(size.pred, value / 2))
      else Bits.i(Bitcons.serialize.uint(size.pred, value / 2))
  }

Bitcons.deserialize.uint.go(size: Nat, bits: Bits): Pair<Bits,Nat>
  case size {
    zero: {bits, 0}
    succ: case bits {
      e: {Bits.e, 0}
      o:
        let {bits, value} = Bitcons.deserialize.uint.go(size.pred, bits.pred)
        {bits, 2 * value}
      i:
        let {bits, value} = Bitcons.deserialize.uint.go(size.pred, bits.pred)
        {bits, 1 + 2 * value}
    }
  }

Bitcons.deserialize.uint(size: Nat, bits: Bits): Nat
  Pair.snd!!(Bitcons.deserialize.uint.go(size, bits))
  
Bitcons.serialize.hash(hash: Bitcons.Hash): Bits
  Bitcons.serialize.uint(256, U256.to_nat(hash))

Bitcons.deserialize.hash.go(bits: Bits): Pair<Bits,Bitcons.Hash>
  let {bits, value} = Bitcons.deserialize.uint.go(256, bits)
  {bits, Nat.to_u256(value)}

Bitcons.deserialize.hash(bits: Bits): Bitcons.Hash
  Pair.snd!!(Bitcons.deserialize.hash.go(bits))

Bitcons.serialize.words(length: Nat, words: Vector<U256,length>): Bits
  case length with words {
    zero: Bits.e
    succ: case words {
      cons: Bits.concat(Bitcons.serialize.uint(256, U256.to_nat(words.head)), Bitcons.serialize.words(length.pred, words.tail))
    }
  }!

Bitcons.deserialize.words.go(length: Nat, bits: Bits): Pair<Bits,Vector<U256,length>>
  case length {
    zero: {bits, Vector.nil!}
    succ: 
      let {bits, head} = Bitcons.deserialize.uint.go(256, bits)
      let {bits, tail} = Bitcons.deserialize.words.go(length.pred, bits)
      {bits, Vector.cons!<length.pred>(Nat.to_u256(head), tail)}
  }!

Bitcons.deserialize.words(length: Nat, bits: Bits): Vector<U256,length>
  Pair.snd!!(Bitcons.deserialize.words.go(length, bits))

Bitcons.serialize.address(address: Bitcons.Address): Bits
  open address
  Bits.flatten([
    Bitcons.serialize.uint(8, U8.to_nat(address.ip0)),
    Bitcons.serialize.uint(8, U8.to_nat(address.ip1)),
    Bitcons.serialize.uint(8, U8.to_nat(address.ip2)),
    Bitcons.serialize.uint(8, U8.to_nat(address.ip3)),
    Bitcons.serialize.uint(16, U16.to_nat(address.port)),
  ])

Bitcons.deserialize.address.go(bits: Bits): Pair<Bits,Bitcons.Address>
  let {bits, ip0} = Bitcons.deserialize.uint.go(8, bits)
  let {bits, ip1} = Bitcons.deserialize.uint.go(8, bits)
  let {bits, ip2} = Bitcons.deserialize.uint.go(8, bits)
  let {bits, ip3} = Bitcons.deserialize.uint.go(8, bits)
  let {bits, port} = Bitcons.deserialize.uint.go(16, bits)
  {bits, Bitcons.Address.ipv4(Nat.to_u8(ip0), Nat.to_u8(ip1), Nat.to_u8(ip2), Nat.to_u8(ip3), Nat.to_u16(port))}

Bitcons.deserialize.address(bits: Bits): Bitcons.Address
  Pair.snd!!(Bitcons.deserialize.address.go(bits))

Bitcons.serialize.many<A: Type>(item: A -> Bits, list: List<A>): Bits
  case list {
    nil: Bits.o(Bits.e)
    cons: Bits.i(Bits.concat(item(list.head), Bitcons.serialize.many<A>(item, list.tail)))
  }

Bitcons.deserialize.many.go<A: Type>(item: Bits -> Pair<Bits,A>, bits: Bits): Pair<Bits,List<A>>
  case bits {
    e:
      {Bits.e, []}
    o:
      {bits.pred, []}
    i:
      let bits = bits.pred
      let {bits, head} = item(bits)
      let {bits, tail} = Bitcons.deserialize.many.go<A>(item, bits)
      {bits, head & tail}
  }

Bitcons.deserialize.many<A: Type>(item: Bits -> Pair<Bits,A>, bits: Bits): List<A>
  Pair.snd!!(Bitcons.deserialize.many.go<A>(item, bits))

Bitcons.serialize.post(post: Bitcons.Post): Bits
  open post
  Bits.flatten([
    Bitcons.serialize.words(40, post.body),
    Bitcons.serialize.uint(256, U256.to_nat(post.work)),
    Bitcons.serialize.uint(256, U256.to_nat(post.prev)),
  ])

Bitcons.deserialize.post.go(bits: Bits): Pair<Bits,Bitcons.Post>
  let {bits,body} = Bitcons.deserialize.words.go(40, bits)
  let {bits,work} = Bitcons.deserialize.uint.go(256, bits)
  let {bits,prev} = Bitcons.deserialize.uint.go(256, bits)
  {bits, Bitcons.Post.new(body, Nat.to_u256(work), Nat.to_u256(prev))}

Bitcons.deserialize.post(bits: Bits): Bitcons.Post
  Pair.snd!!(Bitcons.deserialize.post.go(bits))

Bitcons.serialize.message(message: Bitcons.Message): Bits
  case message {
    ping: 
      Bits.concat(Bitcons.serialize.uint(8,0), Bitcons.serialize.many!(Bitcons.serialize.address, message.addresses))
    request_post:
      Bits.concat(Bitcons.serialize.uint(8,1), Bitcons.serialize.uint(256, U256.to_nat(message.hash)))
    share_post:
      Bits.concat(Bitcons.serialize.uint(8,2), Bitcons.serialize.post(message.post))
  }

Bitcons.deserialize.message.go(bits: Bits): Pair<Bits,Bitcons.Message>
  let {bits, variant} = Bitcons.deserialize.uint.go(4,bits)
  switch Nat.eql(variant) {
    0:
      let {bits, addresses} = Bitcons.deserialize.many.go!(Bitcons.deserialize.address.go, bits)
      {bits, Bitcons.Message.ping(addresses)}
    1: 
      let {bits, hash} = Bitcons.deserialize.uint.go(256, bits)
      {bits, Bitcons.Message.request_post(Nat.to_u256(hash))}
    2:
      let {bits, post} = Bitcons.deserialize.post.go(bits)
      {bits, Bitcons.Message.share_post(post)}
  } default {bits, Bitcons.Message.ping([])}

Bitcons.deserialize.message(bits: Bits): Bitcons.Message
  Pair.snd!!(Bitcons.deserialize.message.go(bits))
  
Bitcons.Hash.serializer: Serializer<Bitcons.Hash>
  U256.serializer

Bitcons.Hash.deserializer: Deserializer<Bitcons.Hash>
  U256.deserializer

Bitcons.Body.deserializer: Deserializer<Bitcons.Body>
  Vector.deserializer!(40,U256.deserializer)

Bitcons.Body.serializer: Serializer<Bitcons.Body>
  Vector.serializer!(40,U256.serializer)

Bitcons.Message.serialize(message: Bitcons.Message): String
  let hex = Bits.hex.encode(Serializer.run!(Bitcons.Message.serializer, message))
  let hex = if Nat.mod(String.length(hex),2) =? 0 then hex else hex | "0"
  hex

Bitcons.Message.deserialize(string: String): Maybe<Bitcons.Message>
  Deserializer.run!(Bitcons.Message.deserializer, Bits.hex.decode(string))

Bitcons.Message.show(message: Bitcons.Message): String
  case message {
    ping: "ping"
    share_post: "share_post(" | Bitcons.Hash.show(Bitcons.Hash.from_post(message.post)) | ")"
    request_post: "request_post(" | Bitcons.Hash.show(message.hash) | ")"
  }

// Takes the Keccak hash of a list of words
Bitcons.Hash.from_words(words: List<U256>): Bitcons.Hash
  Crypto.Keccak.hash.words(words)

Bitcons.Hash.show(hash: Bitcons.Hash): String
  String.pad_left(64, '0', Nat.hex.encode(U256.to_nat(hash)))

// Returns the PostHash
Bitcons.Hash.from_post(post: Bitcons.Post): Bitcons.Hash
  use post = post
  if U256.eql(post.prev, 0) && U256.eql(post.work, 0) then
    0#256
  else
    let words = Vector.to_list!(40, post.body) ++ [post.work, post.prev]
    Bitcons.Hash.from_words(words)

Bitcons.Message.serialize(message: Bitcons.Message): String
  let hex = Bits.hex.encode(Serializer.run!(Bitcons.Message.serializer, message))
  let hex = if Nat.mod(String.length(hex),2) =? 0 then hex else hex | "0"
  hex

Bitcons.Message.deserialize(string: String): Maybe<Bitcons.Message>
  Deserializer.run!(Bitcons.Message.deserializer, Bits.hex.decode(string))

// Estimates how many attempts were needed to make this hash
Bitcons.Hash.score(hash: U256): U256
  if U256.eql(hash,0) then
    0
  else
    Nat.to_u256(Nat.pow(2,256) / U256.to_nat(hash))

// Increases the Post nonce until it is above target_score
Bitcons.Post.mine(post: Bitcons.Post, target_score: U256, attempts: U256): Maybe<Bitcons.Post>
  let post_score = Bitcons.Hash.score(Bitcons.Hash.from_post(post))
  //log("Mining. Score: " | U256.hex.encode(Bitcons.Hash.from_post(post)) | " " | U256.show(post_score))
  if U256.eql(attempts,0) then
    none
  else if U256.gte(post_score, target_score) then
    some(post)
  else
    Bitcons.Post.mine(post@work <- post@work + 1, target_score, attempts - 1)

// Adds a post to the chain, updating the score, children and tip
// If the prev is null, it is added to a pending map and re-added later
Bitcons.Store.add_post(post: Bitcons.Post, chain: Bitcons.Store): Bitcons.Store
  open post
  open chain
  open chain.tip
  let post_hash = Bitcons.Hash.from_post(post)
  case chain.post{U256.show(post_hash)} as got_post {
    some: // Post already included: do nothing
      chain
    none: // Post not included
      let prev_hash = post.prev
      case chain.post{U256.show(prev_hash)} as got_prev {
        some: // Parent included: add post and compute infos
          let prev_score     = chain.score{U256.show(prev_hash)} abort chain
          let post_score     = U256.add(prev_score, Bitcons.Hash.score(post_hash))
          let prev_children  = chain.children{U256.show(prev_hash)} <> []
          let pendings       = chain.pending{U256.show(post_hash)} <> []
          let chain.post     = chain.post{U256.show(post_hash)} <- post
          let chain.seen     = chain.seen{U256.show(post_hash)} <- unit
          let chain.score    = chain.score{U256.show(post_hash)} <- post_score
          let chain.children = chain.children{U256.show(prev_hash)} <- post_hash & prev_children
          let chain.tip      = if post_score >? Pair.fst!!(chain.tip) then {post_score,post_hash} else chain.tip
          let chain.pending  = Map.delete!(U256.show(post_hash), chain.pending)
          let chain          = Bitcons.Store.new(chain.post, chain.seen, chain.children, chain.pending, chain.score, chain.tip)
          let chain          = for pending in pendings: Bitcons.Store.add_post(pending, chain)
          chain
        none: // Parent not included
          case chain.seen{U256.show(post_hash)} as seen_post {
            some:
              // Post seen before: do nothing
              chain
            none:
              // Brand new post: add to pending list
              let pending_list  = post & (chain.pending{U256.show(prev_hash)} <> []) // TODO: avoid adding duplicates
              let chain.seen    = chain.seen{U256.show(post_hash)} <- unit
              let chain.pending = chain.pending{U256.show(prev_hash)} <- pending_list
              Bitcons.Store.new(chain.post, chain.seen, chain.children, chain.pending, chain.score, chain.tip)
          }
      }
  }

// The initial chain state
Bitcons.Store.genesis: Bitcons.Store
  let chain.post = {U256.show(0): Bitcons.Post.new(Vector.create!(40,()0),0,0)}
  let chain.seen = {}
  let chain.score = {U256.show(0): 0#256}
  let chain.pending = {}
  let chain.children = {U256.show(0): []}
  let chain.tip = {0#256, 0#256}
  Bitcons.Store.new(chain.post, chain.seen, chain.children, chain.pending, chain.score, chain.tip)

// Returns the longest chain, i.e., the one with most accumulated work
Bitcons.Store.longest_chain(chain: Bitcons.Store): List<Bitcons.Post>
  open chain
  open chain.tip
  case chain.post{U256.show(Pair.snd!!(chain.tip))} as tip_post {
    none: []
    some: Bitcons.Store.longest_chain.go(tip_post.value, chain.post, [])
  }

Bitcons.Store.longest_chain.go(
  post: Bitcons.Post
  posts: Map<Bitcons.Post>
  result: List<Bitcons.Post>
): List<Bitcons.Post>
  open post
  let result = List.cons!(post, result)
  if U256.eql(post.prev, 0) then
    result
  else case posts{U256.show(post.prev)} as prev {
    none: result
    some: Bitcons.Store.longest_chain.go(prev.value, posts, result)
  }

// Converts the IP of an address to a string
Bitcons.Address.show.ip(addr: Bitcons.Address): String
  open addr
  let ip0 = Nat.show(U8.to_nat(addr.ip0))
  let ip1 = Nat.show(U8.to_nat(addr.ip1))
  let ip2 = Nat.show(U8.to_nat(addr.ip2))
  let ip3 = Nat.show(U8.to_nat(addr.ip3))
  ip0 | "." | ip1 | "." | ip2 | "." | ip3

// Converts the port of an address to a string
Bitcons.Address.show.port(addr: Bitcons.Address): String
  open addr
  Nat.show(U16.to_nat(addr.port))

// Converts an address to a string
Bitcons.Address.show(addr: Bitcons.Address): String
  Bitcons.Address.show.ip(addr) | ":" | Bitcons.Address.show.port(addr)

Bitcons.Peer.show(peer: Bitcons.Peer): String
  open peer
  "<" | Bitcons.Address.show(peer.address) | ">"

Bitcons.Peer.map_from_list(peers: List<Bitcons.Peer>): Map<Bitcons.Peer>
  Map.from_list!(List.mapped!(peers)!((peer) { Bitcons.Address.show(peer@address), peer }))

Bitcons.Received.from_udp_message(time: Nat, recv: IO.recv_udp.Message): Maybe<Bitcons.Received>
  Maybe {
    open recv
    open recv.from
    let ip = List.mapped!(String.split(recv.from.ip, "."))!((x) Nat.to_u8(Nat.read(x)))
    let port = Nat.to_u16(recv.from.port)
    get ip0 = ip[0]
    get ip1 = ip[1]
    get ip2 = ip[2]
    get ip3 = ip[3]
    let peer = Bitcons.Peer.new(time, Bitcons.Address.ipv4(ip0, ip1, ip2, ip3, port))
    get msge = Bitcons.Message.deserialize(recv.data)
    return Bitcons.Received.new(peer, msge)
  }

Bitcons.Node.send(node: Bitcons.Node, peer: Bitcons.Peer, message: Bitcons.Message): IO<Unit>
  open node
  open peer
  open peer.address
  let from_port = node.port
  let to_ip     = Bitcons.Address.show.ip(peer.address)
  let to_port   = U16.to_nat(peer.address.port)
  //log("SENDING: " | Bitcons.Message.serialize(message))
  IO.send_udp(from_port, to_ip, to_port, Bitcons.Message.serialize(message))

Bitcons.Node.get_random_peers(node: Bitcons.Node, count: Nat): IO<List<Bitcons.Peer>>
  open node
  IO {
    get time = IO.get_time
    use rand = List.get_randoms!(time, count, Map.values!(node.peers))
    return rand.result
  }

Bitcons.Node.send_to_random_peers(node: Bitcons.Node, count: Nat, message: Bitcons.Message): IO<Unit>
  IO {
    get peers = Bitcons.Node.get_random_peers(node, count)
    Bitcons.Node.broadcast.go(node, message, peers)
  }

Bitcons.Node.broadcast(node: Bitcons.Node, message: Bitcons.Message): IO<Unit>
  Bitcons.Node.broadcast.go(node, message, Map.values!(node@peers))

Bitcons.Node.broadcast.go(node: Bitcons.Node, message: Bitcons.Message, peers: List<Bitcons.Peer>): IO<Unit>
  case peers {
    nil: IO.pass
    cons: IO {
      Bitcons.Node.send(node, peers.head, message)
      Bitcons.Node.broadcast.go(node, message, peers.tail)
    }
  }

// Bitcons Node
// ================

// Starts a Bitcons node at given port
Bitcons.Node.init(port: Nat): IO<Unit>
  IO {
    get time = IO.get_time
    let peers = Bitcons.Peer.map_from_list([
      Bitcons.Peer.new(time, Bitcons.Address.ipv4(127,0,0,1,42000))
      Bitcons.Peer.new(time, Bitcons.Address.ipv4(127,0,0,1,42001))
      Bitcons.Peer.new(time, Bitcons.Address.ipv4(127,0,0,1,42002))
      Bitcons.Peer.new(time, Bitcons.Address.ipv4(127,0,0,1,42003))
    ])
    let node = Bitcons.Node.new(port, peers, Bitcons.Store.genesis)
    Bitcons.Node.loop(node)
  }
  
Bitcons.Node.loop(node: Bitcons.Node): IO<Unit>
  IO {
    // Displays UI
    Bitcons.Node.display(node)
    IO.print("")

    // Handle incoming messages
    get inbox = IO.recv_udp(node@port)
    get node = Bitcons.Node.handle_inbox(node, inbox)

    // Attempts to mine a post and adds it if successful
    get time = IO.get_time
    let time = Nat.to_u256(time)
    let body = Vector.create!(40, (i) if i =? 39 then time else 0#256)
    let post = Bitcons.Post.new(body, 0, node@store@tip@snd)
    let node = Bitcons.Node.mine(node, post) <> node

    // Pings random neighbors, sending random peers to them
    Bitcons.Node.ping(node)

    // Shares our best block with random neighbors
    Bitcons.Node.share_tip(node)

    // Requests contents of pending posts
    Bitcons.Node.request_pendings(node)

    // Forgets peers that look inactive
    get node = Bitcons.Node.forget_inactive_peers(node)

    // Loop
    IO.sleep(2000)
    Bitcons.Node.loop(node)

    return unit
  }

Bitcons.Node.display(node: Bitcons.Node): IO<Unit>
  open node
  open node.store
  let posts = Bitcons.Store.longest_chain(node.store)
  //let posts = Map.values!(node.store.post)
  IO {
    IO.clear
    get time = IO.get_time
    IO.print("~~ String:Chain ~~")
    IO.print("Time     : " | Nat.show(time))
    IO.print("Peers    : " | Nat.show(List.length!(Map.to_list!(node.peers))))
    IO.print("Pendings : " | Nat.show(List.length!(Map.to_list!(node.store.pending))))
    let posts_length = List.length!(posts)
    let posts_indexed = List.index!(posts)
    IO.print("index  | body[0]                                                         | hash                                                             | score") 
    for index_post in posts_indexed: IO {
      let {index, post} = index_post
      let half_count = 6
      if Nat.between(half_count, index, posts_length - half_count) then
        if index =? 4
        then IO.print("(...)")
        else IO.pass
      else IO {
        open post
        let post_hash = Bitcons.Hash.from_post(post)
        let score = node.store.score{U256.show(post_hash)} <> 0#256
        let show_index = Nat.show(index)
        let show_body = String.hex.decode(Nat.hex.encode(U256.to_nat(Vector.head!!(post.body))))
        //let show_body = List.show!(U256.show,Vector.to_list!(32,post.body))
        let show_hash = Bitcons.Hash.show(post_hash)
        //let show_hash = Bitcons.Hash.show(post.prev)
        let show_score = U256.show(score)
        IO.print(""
          | String.pad_left_exact(6, '0', show_index) | " | "
          | String.pad_left_exact(64, '0', show_body) | " | "
          | String.pad_left_exact(64, '0', show_hash) | " | "
          | String.pad_left_exact(8, '0', show_score))
        //IO.print("[" | Nat.show(index) | "] " | Bitcons.Hash.show(post_hash) | " -- score: " | U256.show(score))
      }
    }
  }

Bitcons.Node.on_message(node: Bitcons.Node, received: Bitcons.Received): IO<Bitcons.Node>
  open received
  IO {
    get node = Bitcons.Node.refresh_peer(node, received.sender)
    open node
    open node.store
    case received.message {
      ping: IO {
        //log("~ received ping")
        //Bitcons.Node.send(node, received.sender, Bitcons.Message.pong)
        return node
      }
      request_post: 
        //log("~ received request")
        case node.store.post{U256.show(received.message.hash)} as got {
          none: IO {
            return node
          }
          some: IO {
            Bitcons.Node.send(node, received.sender, Bitcons.Message.share_post(got.value))
            return node
          }
        }
      share_post: IO {
        //log("~ received post")
        return Bitcons.Node.add_post(node, received.message.post)
      }
    } default IO {
      return node
    }
  }

Bitcons.Node.refresh_peer(node: Bitcons.Node, peer: Bitcons.Peer): IO<Bitcons.Node>
  open node
  open peer
  IO {
    get time = IO.get_time
    let peer = peer@last_seen <- time
    let node = node@peers <- (node.peers{Bitcons.Peer.show(peer)} <- peer)
    return node
  }

Bitcons.Node.add_post(node: Bitcons.Node, post: Bitcons.Post): Bitcons.Node
  open node
  node@store <- Bitcons.Store.add_post(post, node.store)

Bitcons.Node.handle_inbox(node: Bitcons.Node, inbox: List<IO.recv_udp.Message>): IO<Bitcons.Node>
  IO {
    IO.print("Handling incoming UDP messages...")
    Bitcons.Node.handle_inbox.go(node, inbox)
  }

Bitcons.Node.handle_inbox.go(node: Bitcons.Node, inbox: List<IO.recv_udp.Message>): IO<Bitcons.Node>
  case inbox {
    nil: IO {
      return node
    }
    cons: IO {
      open inbox.head
      get time = IO.get_time
      use received = Bitcons.Received.from_udp_message(time, inbox.head)
      case received {
        none: Bitcons.Node.handle_inbox.go(node, inbox.tail)
        some: IO {
          use received = received.value
          get node = Bitcons.Node.on_message(node, received)
          get node = Bitcons.Node.handle_inbox.go(node, inbox.tail)
          IO.print("- " | Bitcons.Peer.show(received.sender) | " " | Bitcons.Message.show(received.message))
          return node
        }
      }
    }
  }

Bitcons.Node.forget_inactive_peers(node: Bitcons.Node): IO<Bitcons.Node>
  open node
  IO {
    get time = IO.get_time
    let peers = Map.values!(node.peers)
    let peers = List.filter<Bitcons.Peer>((peer) (peer@last_seen + 10000) >? time, peers) 
    return node@peers <- Bitcons.Peer.map_from_list(peers)
  }

Bitcons.Node.ping(node: Bitcons.Node): IO<Unit>
  IO {
    log("Pinging peers...")
    get peers = Bitcons.Node.get_random_peers(node, 8)
    let message = Bitcons.Message.ping(List.mapped!(peers)!((peer) peer@address))
    Bitcons.Node.broadcast(node, message)
  }

Bitcons.Node.share_tip(node: Bitcons.Node): IO<Unit>
  open node
  open node.store
  IO {
    log("Sharing tip with peers...")
    let tip_post = node.store.post{U256.show(Pair.snd!!(node.store.tip))} abort IO.pass
    let message = Bitcons.Message.share_post(tip_post)
    Bitcons.Node.broadcast(node, message)
  }

Bitcons.Node.request_pendings(node: Bitcons.Node): IO<Unit>
  IO {
    log("Requesting pendings...")
    open node
    open node.store
    let pendings = List.map!!(U256.read, Map.keys!(node.store.pending))
    for pending in pendings:
      case node.store.seen{U256.show(pending)} as seen {
        none: 
          let message = Bitcons.Message.request_post(pending)
          Bitcons.Node.broadcast(node, message)
        some:
          IO.pass
      }
  }

Bitcons.Node.mine(node: Bitcons.Node, post: Bitcons.Post): Maybe<Bitcons.Node>
  IO {
    log("Mining post...")
    open node
    open node.store
    let mined_post = Bitcons.Post.mine(post, 32, 64)
    case mined_post {
      none: log("- Failure.") none
      some: log("- Success!") some(Bitcons.Node.add_post(node, mined_post.value))
    }
  }
