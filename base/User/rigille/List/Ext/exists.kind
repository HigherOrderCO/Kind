User.rigille.List.Ext.exists<A: Type>(after: A -> A -> Type, O: User.rigille.Order<A>(after),
  xs: List<A>,
  Hyp: Not(List.length<A>(xs) == 0)
): [x: A] [H: User.rigille.List.In<A>(xs, x)] User.rigille.List.Ext<A, after, O>(xs, x)
  case xs with Hyp {
    nil: Empty.absurd!(Hyp(refl))
    cons: case xs.tail with Hyp {
            nil: head_in = Or.left!!(refl) :: User.rigille.List.In<A>([xs.head], xs.head)
                 open O
                 prop = (y, Hyp) case Hyp {
                          left: qed = O.refl(y)
                                qed = qed :: rewrite X in after(X, y) with Hyp.value
                                qed
                          right: Empty.absurd!(Hyp.value)
                        }
                 xs.head ~ head_in ~ prop
            cons: open O
                  not_empty = Nat.succ_neq_zero(List.length<A>(xs.tail.tail))
                  ind = User.rigille.List.Ext.exists<A, after, O>(xs.tail.head & xs.tail.tail, not_empty)
                  open ind
                  open ind.snd
                  case O.conn(xs.head, ind.fst) as ord {
                    left: head_in = Or.left!!(refl) :: User.rigille.List.In<A>(xs.head & xs.tail.head & xs.tail.tail, xs.head)
                          head_ext = ((y, Hyp) case Hyp {
                                                 left: duh = O.refl(y)
                                                       duh :: rewrite X in after(X, y) with Hyp.value
                                                 right: smaller = ind.snd.snd(y, Hyp.value)
                                                        O.chain!!!(ord.value, smaller)
                                               }!)
                          xs.head ~ head_in ~ head_ext
                    right: ind_in = Or.right!!(ind.snd.fst) :: User.rigille.List.In<A>(xs.head & xs.tail.head & xs.tail.tail, ind.fst)
                           ind_ext = ((y, Hyp) case Hyp {
                                                 left: qed = ord.value :: rewrite X in after(ind.fst, X) with mirror(Hyp.value)
                                                       qed
                                                 right: ind.snd.snd(y, Hyp.value)
                                               }!)
                           ind.fst ~ ind_in ~ ind_ext
                  }
          }!
  }!
