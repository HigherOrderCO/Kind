// How to use this file:
// 1. Clone Kind's repo:     git clone https://github.com/uwu-tech/kind
// 2. Create a dir for you:  mkdir Kind/base/User/YourName
// 3. Copy that file there:  cp Kind/base/User.dvberkel.Problems.kind Kind/base/User/YourName
// 4. Open, uncomment a problem and solve it
// 5. Send a PR if you want!
// If you need help, read the tutorial on Kind/THEOREMS.md, or ask on Telegram.
// Answers: https://github.com/uwu-tech/Kind/blob/master/base/User/MaiaVictor/User.dvberkel.Problems.kind

// -----------------------------------------------------------------------------

// ::::::::::::::
// :: Programs ::
// ::::::::::::::

// Returs true if both inputs are true
User.dvberkel.Problems.p0(a: Bool, b: Bool): Bool
  case a b {
      true  true  : true
      false true  : false
      true  false : false
      false false : false
  }

// Returs true if any input is true
User.dvberkel.Problems.p1(a: Bool, b: Bool): Bool
  case a b {
      true  true  : true
      false true  : true
      true  false : true
      false false : false
  }

// Returs true if both inputs are identical
User.dvberkel.Problems.p2(a: Bool, b: Bool): Bool
  case a b {
      true  true  : true
      false true  : false
      true  false : false
      false false : true
  }

// Returns the first element of a pair
User.dvberkel.Problems.p3<A: Type, B: Type>(pair: Pair<A,B>): A
  open pair
  pair.fst

// Returns the second element of a pair
User.dvberkel.Problems.p4<A: Type, B: Type>(pair: Pair<A,B>): B
  open pair
  pair.snd

// Inverses the order of the elements of a pair
User.dvberkel.Problems.p5<A: Type, B: Type>(pair: Pair<A,B>): Pair<B,A>
  open pair
  Pair.new!!(pair.snd, pair.fst)

// Applies a function to both elements of a Pair
User.dvberkel.Problems.p6<A: Type, B: Type>(fn: A -> B, pair: Pair<A,A>): Pair<B,B>
  open pair
  Pair.new!!(fn(pair.fst), fn(pair.snd))

// Doubles a number
User.dvberkel.Problems.p7(n: Nat): Nat
  case n {
    zero: Nat.zero
    succ: Nat.succ(Nat.succ(User.dvberkel.Problems.p7(n.pred)))
  }

// Halves a number, rounding down
User.dvberkel.Problems.p8(n: Nat): Nat
  case n {
    zero: Nat.zero
    succ:
      case n.pred {
        zero: Nat.zero
        succ: User.dvberkel.Problems.p8(n.pred.pred)
      }
  }

// Adds two numbers
User.dvberkel.Problems.p9(a: Nat, b: Nat): Nat
  case a {
    zero: b
    succ: Nat.succ(User.dvberkel.Problems.p9(a.pred, b))
  }

// Subtracts two numbers
User.dvberkel.Problems.p10(a: Nat, b: Nat): Nat
  case b {
    zero: a
    succ:
      case a {
        zero: Nat.zero
        succ: User.dvberkel.Problems.p10(a.pred, b.pred)
      }
  }

// Multiplies two numbers
User.dvberkel.Problems.p11(a: Nat, b: Nat): Nat
  case a {
    zero: Nat.zero
    succ: User.dvberkel.Problems.p9(b, User.dvberkel.Problems.p11(a.pred, b))
  }

// Returns true if a < b
User.dvberkel.Problems.p12(a: Nat, b: Nat): Bool
  case a {
    zero:
      case b {
        zero: Bool.false
        succ: Bool.true
      }
    succ:
      case b {
          zero: Bool.false
          succ: User.dvberkel.Problems.p12(a.pred, b.pred)
      }    
  }

// Returns true if a == b
User.dvberkel.Problems.p13(a: Nat, b: Nat): Bool
  case a b {
    zero zero: Bool.true
    zero succ: Bool.false
    succ zero: Bool.false
    succ succ: User.dvberkel.Problems.p13(a.pred, b.pred)
  }

// Returns the first element of a List
User.dvberkel.Problems.p14<A: Type>(xs: List<A>): Maybe<A>
  case xs {
      nil: Maybe.none!
      cons: Maybe.some!(xs.head)
  }

// Returns the list without the first element
User.dvberkel.Problems.p15<A: Type>(xs: List<A>): List<A>
  case xs {
      nil: List.nil!
      cons: xs.tail
  }

// Returns the length of a list
User.dvberkel.Problems.p16<A: Type>(xs: List<A>): Nat
  case xs {
      nil: Nat.zero
      cons: Nat.succ(User.dvberkel.Problems.p16!(xs.tail))
  }

// Concatenates two lists
User.dvberkel.Problems.p17<A: Type>(xs: List<A>, ys: List<A>): List<A>
  case xs {
      nil: ys
      cons: List.cons!(xs.head, User.dvberkel.Problems.p17!(xs.tail, ys))
  }

// Applies a function to all elements of a list
User.dvberkel.Problems.p18<A: Type, B: Type>(fn: A -> B, xs: List<A>): List<B>
  case xs {
      nil: List.nil!
      cons: List.cons!(fn(xs.head), User.dvberkel.Problems.p18!!(fn, xs.tail))
  }

// Returns the same list, with the order reversed
User.dvberkel.Problems.p19<A: Type>(xs: List<A>): List<A>
  User.dvberkel.Problems.p19_helper!([], xs)

User.dvberkel.Problems.p19_helper<A: Type>(accumulator: List<A>, xs: List<A>): List<A>
  case xs {
      nil: accumulator
      cons: User.dvberkel.Problems.p19_helper!(List.cons!(xs.head, accumulator), xs.tail)
  }

// Returns pairs of the elements of the 2 input lists on the same index
// Ex: User.dvberkel.Problems.p20!!([1,2], ["a","b"]) == [{1,"a"},{2,"b"}]
User.dvberkel.Problems.p20<A: Type, B: Type>(xs: List<A>, ys: List<B>): List<Pair<A,B>>
  case xs {
      nil: List.nil!
      cons:
        case ys {
            nil: List.nil!
            cons: List.cons!(Pair.new!!(xs.head, ys.head), User.dvberkel.Problems.p20!!(xs.tail, ys.tail))
        }
  }

// Returns the smallest element of a List
User.dvberkel.Problems.p21(xs: List<Nat>): Maybe<Nat>
  case xs {
      nil: Maybe.none!
      cons: Maybe.some!(User.dvberkel.Problems.p21_helper(xs.head, xs.tail))
  }

User.dvberkel.Problems.p21_helper(x: Nat, xs: List<Nat>): Nat
  case xs {
      nil: x
      cons:
        case Nat.lte(x, xs.head) {
            true: User.dvberkel.Problems.p21_helper(x, xs.tail)
            false: User.dvberkel.Problems.p21_helper(xs.head, xs.tail)
        }
  }

// Returns the same list without the smallest element
User.dvberkel.Problems.p22(xs: List<Nat>): List<Nat>
  case xs {
      nil: List.nil!
      cons:
        let minimum = User.dvberkel.Problems.p21_helper(xs.head, xs.tail)
        User.dvberkel.Problems.p22_helper([], minimum, xs)
  }

User.dvberkel.Problems.p22_helper(accumulator: List<Nat>, to_remove: Nat, xs: List<Nat>): List<Nat>
  case xs {
      nil: User.dvberkel.Problems.p19!(accumulator)
      cons:
        case Nat.eql(to_remove, xs.head) {
            true: List.concat!(User.dvberkel.Problems.p19!(accumulator), xs.tail)
            false: User.dvberkel.Problems.p22_helper(List.cons!(xs.head, accumulator), to_remove, xs.tail)
        }
  }

// Returns the same list, in ascending order
User.dvberkel.Problems.p23(xs: List<Nat>): List<Nat>
  let minimum = User.dvberkel.Problems.p21(xs)
  let rest = User.dvberkel.Problems.p22(xs)
  case minimum {
      none: xs
      some: List.cons!(minimum.value, User.dvberkel.Problems.p23(rest))
  }

// -----------------------------------------------------------------------------

// ::::::::::::::
// :: Theorems ::
// ::::::::::::::

// Note: these problems use functions from the base libs, NOT the ones above

User.dvberkel.Problems.t0: true == true
  refl

User.dvberkel.Problems.t1(a: Bool): Bool.and(false, a) == false
  refl

User.dvberkel.Problems.t2(a: Bool): Bool.and(a, false) == false
  case a {
      true: refl
      false: refl
  }!

User.dvberkel.Problems.t3(a: Bool): Bool.or(true, a) == true
  refl

User.dvberkel.Problems.t4(a: Bool): Bool.or(a, true) == true
  case a {
      true: refl
      false: refl
  }!

User.dvberkel.Problems.t5(a: Bool): Bool.eql(a, a) == true
  case a {
      true: refl
      false: refl
  }!

User.dvberkel.Problems.t6(a: Bool): Bool.not(Bool.not(a)) == a
  case a {
      true: refl
      false: refl
  }!

User.dvberkel.Problems.t7(a: Bool, b: Bool): Bool.not(Bool.and(a,b)) == Bool.or(Bool.not(a), Bool.not(b))
  case a b {
      true  true: refl
      false true: refl
      true  false: refl
      false false: refl
  }!

User.dvberkel.Problems.t8(a: Bool, b: Bool): Bool.not(Bool.or(a,b)) == Bool.and(Bool.not(a), Bool.not(b))
  case a b {
      true  true: refl
      false true: refl
      true  false: refl
      false false: refl
  }!

User.dvberkel.Problems.t9(a: Pair<Nat,Nat>): Pair.new<Nat,Nat>(Pair.fst<Nat,Nat>(a), Pair.snd<Nat,Nat>(a)) == a
  case a {
      new: refl
  }!

User.dvberkel.Problems.t10(a: Pair<Nat,Nat>): Pair.swap<Nat,Nat>(Pair.swap<Nat,Nat>(a)) == a
  case a {
      new: refl
  }! 

User.dvberkel.Problems.t11(n: Nat): Nat.same(n) == n
 case n {
     zero: refl
     succ:
       let ind = User.dvberkel.Problems.t11(n.pred)
       let qed = apply(Nat.succ, ind)
       qed
 }!

User.dvberkel.Problems.t12(n: Nat): Nat.half(Nat.double(n)) == n
  case n {
      zero: refl
      succ:
        let ind = User.dvberkel.Problems.t12(n.pred)
        let qed = apply(Nat.succ, ind)
        qed
  }!

User.dvberkel.Problems.t13(n: Nat): Nat.add(0,n) == n
  refl

User.dvberkel.Problems.t14(n: Nat): Nat.add(n,0) == n
  case n {
      zero: refl
      succ:
        let ind = User.dvberkel.Problems.t14(n.pred)
        let qed = apply(Nat.succ, ind)
        qed
  }!

User.dvberkel.Problems.t15(n: Nat, m: Nat): Nat.add(Nat.succ(n),m) == Nat.succ(Nat.add(n,m))
 case n {
     zero: refl
     succ:
        let ind = User.dvberkel.Problems.t15(n.pred, m)
        let qed = apply(Nat.succ, ind)
        qed
 }!
  
User.dvberkel.Problems.t16(n: Nat, m: Nat): Nat.add(n,Nat.succ(m)) == Nat.succ(Nat.add(n,m))
  case n {
      zero: refl
      succ:
        let ind = User.dvberkel.Problems.t16(n.pred, m)
        let qed = apply(Nat.succ, ind)
        qed
  }!

User.dvberkel.Problems.t17(n: Nat, m: Nat): Nat.add(n, m) == Nat.add(m, n)
 case m {
   zero: User.dvberkel.Problems.t14(n)
   succ:
     let ind = User.dvberkel.Problems.t17(n, m.pred)
     let lemma = apply(Nat.succ, ind)
     let left = User.dvberkel.Problems.t16(n, m.pred)
     let qed = Equal.chain!!!!(left, lemma)
     qed
 }!

User.dvberkel.Problems.t18(n: Nat): Nat.add(n,n) == Nat.double(n)
 case n {
   zero: refl
   succ:
     let ind = User.dvberkel.Problems.t18(n.pred)
     let lemma_0 = apply(Nat.succ, ind)
     let left = User.dvberkel.Problems.t16(n.pred, n.pred)
     let lemma_1 = Equal.chain!!!!(left, lemma_0)
     let qed = apply(Nat.succ, lemma_1)
     qed
 }!

User.dvberkel.Problems.t19(n: Nat): Nat.ltn(n, Nat.succ(n)) == true
 case n {
   zero: refl
   succ:
     let qed = User.dvberkel.Problems.t19(n.pred)
     qed
 }!

User.dvberkel.Problems.t20(n: Nat): Nat.gtn(Nat.succ(n), n) == true
 case n {
   zero: refl
   succ:
     let qed = User.dvberkel.Problems.t20(n.pred)
     qed
 }!

User.dvberkel.Problems.t21(n: Nat): Nat.sub(n,n) == 0
 case n {
   zero: refl
   succ:
     let qed = User.dvberkel.Problems.t21(n.pred)
     qed
 }!

User.dvberkel.Problems.t22(n: Nat, e: n == 1): Nat.succ(n) == 2
  apply(Nat.succ, e)

User.dvberkel.Problems.t23(n: Nat, m: Nat, e: Nat.eql(n,m) == true): n == m
 case n m with e {
   zero zero: refl
   zero succ:
     let impossible = Bool.false_neq_true(e)
     Empty.absurd!(impossible)
   succ zero:
     let impossible = Bool.false_neq_true(e)
     Empty.absurd!(impossible)
   succ succ:
     let trivial = refl :: Nat.eql(n.pred, m.pred) == Nat.eql(Nat.succ(n.pred), Nat.succ(m.pred))
     let lemma = Equal.chain!!!!(trivial, e)
     let ind = User.dvberkel.Problems.t23(n.pred, m.pred, lemma)
     apply(Nat.succ, ind)
 }!

User.dvberkel.Problems.t24(xs: List<Nat>): Nat.gtn(List.length<Nat>(List.cons<Nat>(1,xs)),0) == true
 refl

User.dvberkel.Problems.t25(xs: List<Nat>): List.map<Nat,Nat>((x) x, xs) == xs
 case xs {
   nil: refl
   cons:
     let ind = User.dvberkel.Problems.t25(xs.tail)
     apply(List.cons!(xs.head), ind)
 }!

User.dvberkel.Problems.t26(xs: List<Nat>, ys: List<Nat>): Nat.add(List.length<Nat>(xs), List.length<Nat>(ys)) == List.length<Nat>(List.concat<Nat>(xs,ys))
 case xs {
   nil: refl
   cons:
     let ind = User.dvberkel.Problems.t26(xs.tail, ys)
     apply(Nat.succ, ind)
 }!

// User.dvberkel.Problems.t27(xs: List<Nat>): List.reverse<Nat>(List.reverse<Nat>(xs)) == xs
  // ?a

User.dvberkel.Problems.t28: true != false
 (e)
   let impossible = Bool.true_neq_false(e)
   Empty.absurd!(impossible)

User.dvberkel.Problems.t29.0: 1 != 0
  (e)
    let impossible = Nat.one_neq_zero(e)
    Empty.absurd!(impossible)

User.dvberkel.Problems.t29: 3 != 2
 (e)
   let double_pred = apply(Nat.pred, apply(Nat.pred, e))
   let qed = User.dvberkel.Problems.t29.0(double_pred)
   qed

User.dvberkel.Problems.t30(a: Bool): Bool.or(true, a) != false
  (e)
    let impossible = Bool.true_neq_false(e)
    Empty.absurd!(impossible)

User.dvberkel.Problems.t31(a: Bool): Bool.or(a, true) != false
 (e)
   case a with e {
     true:
       let impossible = Bool.true_neq_false(e)
       Empty.absurd!(impossible)
     false:
       let impossible = Bool.true_neq_false(e)
       Empty.absurd!(impossible)
   }!

User.dvberkel.Problems.t32(a: Bool): Bool.and(false, a) != true
 (e)
   let impossible = Bool.false_neq_true(e)
   Empty.absurd!(impossible)

User.dvberkel.Problems.t33(a: Bool): Bool.and(a, false) != true
 (e)
   case a with e {
     true:
       let impossible = Bool.false_neq_true(e)
       Empty.absurd!(impossible)
     false:
       let impossible = Bool.false_neq_true(e)
       Empty.absurd!(impossible)
   }!

User.dvberkel.Problems.t34(a: Nat, b: Nat, e: a == b): b == a
  case e {
    refl: refl
  }!

User.dvberkel.Problems.t35(a: Nat, b: Nat, c: Nat, e0: a == b, e1: b == c): a == c
  Equal.chain!!!!(e0, e1)

User.dvberkel.Problems.t36(a: Nat, P: Nat -> Type, p: P(a)): P(Nat.same(a))
  let ammel = User.dvberkel.Problems.t11(a)
  let lemma = mirror(ammel)
  let qed = p :: rewrite x in P(x) with lemma
  qed
