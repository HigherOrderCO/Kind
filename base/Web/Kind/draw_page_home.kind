Web.Kind.draw_page_home(stt: Web.Kind.State): DOM
  open stt
  // Auxiliars
  // ----------
  let span =
    ((txt) DOM.node("span", {}, {}, 
    [DOM.text(txt)])) :: String -> DOM

  let line = 
    ((txt) DOM.node("p", {}, {}, 
    [DOM.text(txt)])) :: String -> DOM

  let line_break = DOM.node("br", {}, {}, [])

  let span_bold =
    ((txt) DOM.node("span", {}, Web.Kind.typography.body_strong,
    [DOM.text(txt)])) :: String -> DOM

  // Content
  // ----------
  let go_to_apps_height = case stt.device {
    phone: "6em"
  } default "10em"
  let heading_typography = case stt.device {
    phone: Web.Kind.typography.h2
  } default Web.Kind.typography.h1

  let intro =
    DOM.node("div", {}, Web.Kind.typography.body, [
      span("Kind is a cute ") span_bold("proof")
      span("gramming language.") line_break line_break
      span("It's ") span_bold("capable of everything")
      line("from web apps to games to")
      line("advanced mathematical proofs.")
    ])

  let call_to_apps =
    let croni =
      DOM.node("div", {}, {
        "margin-left": "3em"
        "display": "flex"
        "justify-content": "flex-end"
      }, [
        span("gl hf!")
        DOM.node("img", {"src": Web.Kind.img.croni}, {
          "width": "2em"
          "height": "2em"
        }, [])
      ])
      
    DOM.node("div", {}, {
      "height": go_to_apps_height
      "display": "flex"
      "align-items": "center"
      "justify-content": "center"
    }, [
      Web.Kind.comp.btn_primary_solid("GO TO APPS", "btn_pri_home_go_to_apps"),
      croni
    ])
    // Kaelin.Assets.chars.croni_idle_00
  
  let install =
    let instructions = 
      DOM.node("div", {}, Map.union!(Web.Kind.typography.code, {
        "margin-top": "0.5em"
        "padding": "0.5em"
        "box-shadow": Web.Kind.constant.primary_color | " -2px 2px 1px"
        "border": "1px solid"}), [
          line("npm i -g kind-lang")
          line("git clone https://github.com/uwu-tech/Kind")
          line("cd Kind/base")
          line("kind Main")
          line("kind Main --run")
        ])
    DOM.node("div", {"id": "instructions"}, {}, [
      Web.Kind.comp.heading(heading_typography, "Install"), instructions
    ])
  Web.Kind.comp.page("home", stt, [
        intro,
        call_to_apps,
        install])