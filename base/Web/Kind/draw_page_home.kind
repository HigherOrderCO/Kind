Web.Kind.draw_page_home(stt: Web.Kind.State): DOM
  open stt
  // Auxiliars
  // ----------
  let line = 
    ((txt) DOM.node("p", {}, {"font-size": Web.Kind.constant.p_tag_size}, 
      [DOM.text(txt)])) :: String -> DOM
  
  let line_break = DOM.node("p", {}, {"font-size": Web.Kind.constant.p_tag_size}, 
    [DOM.node("br", {}, {}, [])])

  let span =
    ((txt) DOM.node("span", {}, {"font-size": Web.Kind.constant.p_tag_size}, 
    [DOM.text(txt)])) :: String -> DOM

  let span_bold =
    ((txt) DOM.node("span", {}, {
      "font-weight": "bold"
      "font-size": Web.Kind.constant.p_tag_size
      }, [DOM.text(txt)])) :: String -> DOM

  // Content
  // ----------
  let intro =
    DOM.node("div", {}, {}, [
      span("Kind is a cute ") span_bold("proof")
      span("gramming language.") line_break
      span("It's ") span_bold("capable of everything")
      line("from web apps to games to")
      line("advanced mathematical proofs.")
    ])

  let call_to_apps =
    let croni =
      DOM.node("div", {}, {
        "margin-left": "40px"
        "display": "flex"
        "justify-content": "flex-end"
      }, [
        DOM.node("p", {}, {"margin-bottom": "10px"}, [DOM.text("gl hf!")])
        DOM.node("img", {"src": Web.Kind.img.croni}, {
          "width": "30px"
          "height": "30px"
        }, [])
      ])
      
    DOM.node("div", {}, {
      "height": "100px"
      "display": "flex"
      "align-items": "center"
      "justify-content": "center"
    }, [
      Web.Kind.component.btn_primary_solid("GO TO APPS", "btn_pri_home_go_to_apps"),
      croni
    ])
    // Kaelin.Assets.chars.croni_idle_00
  
  let install =
    let instructions = 
      DOM.node("div", {}, {
        "margin-top": "10px"
        "padding": "5px"
        "border": "1px solid"}, [
          line("npm i -g kind-lang")
          line("git clone https://github.com/uwu-tech/Kind")
          line("cd Kind/base")
          line("kind Main")
          line("kind Main --run")
        ])
    DOM.node("div", {}, {"margin": "20px 0px 20px 0px"}, [
      Web.Kind.component.title("Install"), instructions
    ])

  DOM.node("div", {"id": "page-home"}, {
    "display": "flex"
    "flex-direction": "column",
    "justify-content": "space-between",
    "height": "100%"
  }, [
    Web.Kind.component.header(stt), 
    Web.Kind.component.body_container([
      intro,
      call_to_apps,
      install
    ]),
    Web.Kind.component.footer
  ])