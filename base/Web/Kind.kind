type Web.Kind.State {
  new(
    device: Device, 
    page: Web.Kind.Page,
    mouse_over: String) // element id
}

type Web.Kind.Page {
  home
  apps
}

type Web.Kind.Event {
  go_to_home
  go_to_apps
}
  
Web.Kind.exe_event(id: String, stt: Web.Kind.State): Web.Kind.State
  open stt
  let actions = Web.Kind.comp.id_action
  case Map.get!(id, actions) as event {
    none: stt
    some: 
      case event.value{
        go_to_home: Web.Kind.State.new(stt.device, Web.Kind.Page.home, stt.mouse_over)
        go_to_apps: Web.Kind.State.new(stt.device, Web.Kind.Page.apps, stt.mouse_over)
      } 
  }

Web.Kind.set_mouse_over(id: String, stt: Web.Kind.State): Web.Kind.State
  open stt
  Web.Kind.State.new(stt.device, stt.page, id)

Web.Kind.draw_page(stt: Web.Kind.State): DOM
  open stt
  case stt.page{
    home: Web.Kind.draw_page_home(stt)
    apps: Web.Kind.draw_page_apps(stt)
  }

// Executes the application
Web.Kind: App(Web.Kind.State)

  init = 
    Web.Kind.State.new(Device.big_desktop, Web.Kind.Page.home, "")

  draw = (state)
    open state
    Web.Kind.draw_page(state)

  when = (event, state)
    case event {
      init: 
        open event.info
        open event.info.screen_size as screen_size
        let device = Device.classify(screen_size.fst)
        App.store!(Web.Kind.State.new(device, Web.Kind.Page.home, ""))
      mouse_click: App.store!(Web.Kind.exe_event(event.id, state))
      mouse_over : App.store!(Web.Kind.set_mouse_over(event.id, state))
      resize: 
        open event.info
        open event.info.screen_size as screen_size
        open state
        let device = Device.classify(screen_size.fst)
        App.store!(Web.Kind.State.new(device, state.page, state.mouse_over))
    } default App.pass
  
  // The application
  App.new!(init, draw, when)


