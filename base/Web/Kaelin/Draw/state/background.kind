// Draws a map of hexagons given the style "offset coordinates"
// TODO: Change the hexagon's color based on Web.Kaelin.CastInfo
Web.Kaelin.Draw.state.background(state: Web.Kaelin.State, img: VoxBox, map: Web.Kaelin.Map): VoxBox
  case state{
    game:
      // First, draws all hexagons affected by an upcoming ability or movement and removes them from the pool.
      let img =
      case state.cast_info as cast { 
        none: img
        some: 
          open cast.value
          let range = Web.Kaelin.Coord.Axial.range(cast.value.hero_pos, cast.value.range) // Gets the list of affected hexagons
          for coord_range in range with img:
            let tile    = Web.Kaelin.Map.get(coord_range, map) 
            let terrain = Web.Kaelin.Draw.terrain(coord_range, tile <> [], img, cast.value) 
            let map     = Web.Kaelin.Map.del(coord_range, map) // Deletes that tile from the list
            let {i, j}  = Web.Kaelin.Coord.to_screen_xy(coord_range)
            let i       = U32.sub(i, Web.Kaelin.Constants.hexagon_radius)
            let j       = U32.sub(j, Web.Kaelin.Constants.hexagon_radius)
            VoxBox.Draw.image(i, j, 0#32, terrain, img)
          img
      }
      // Then draws the rest of the tiles
      let list = NatMap.to_list!(map)
        for pair in list with img:
          open pair
          let coord = Web.Kaelin.Coord.Convert.nat_to_axial(pair.fst)
          let {i,   j} = Web.Kaelin.Coord.to_screen_xy(coord)
          let i = U32.sub(i, Web.Kaelin.Constants.hexagon_radius)
          let j = U32.sub(j, Web.Kaelin.Constants.hexagon_radius)
          for entity in pair.snd with img:
            case entity {
              background:
                open entity.terrain
                VoxBox.Draw.image(i, j, 0#32, entity.terrain.normal, img)
            }default img
          img
        img
  }default img
