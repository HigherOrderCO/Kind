// Many container-related syntax sugars

Example.containers.pair: Nat
  let pair  = {1,2} // Pair.new!!(1, 2)
  let {a,b} = pair  // case pair { new: a = pair.fst; b = pair.snd; ... }
  a + b

Example.containers.list: Nat
  let list = [1, 2, 3, 4]   // List.cons!(1, List.cons!(2, ...))
  let list = list[1] <- 200 // List.set!(1, 200, list)
  let val0 = list[1] <> 0   // Maybe.default!(List.get!(1,list), 0)
  let val1 = list[2] <> 0   // Maybe.default!(List.get!(2,list), 0)
  val0 + val1

Example.containers.map: Nat
  let key  = "toe"
  let map  = {"tic": 1, "tac": 2, key: 3} // Map.from_list!([{"tic",1}, ...])
  let map  = map{"tic"} <- 100            // Map.set!("tic", 100, map)
  let map  = map{"tac"} <- 200            // Map.set!("tac", 200, map)
  let map  = map{ key } <- 300            // Map.set!(key, 300, map)
  let val0 = map{"tic"} <> 0              // Maybe.default!(Map.get!("tic",map), 0)
  let val1 = map{"tac"} <> 0              // Maybe.default!(Map.get!("tac",map), 0)
  let val2 = map{ key } <> 0              // Maybe.default!(Map.get!(key, map), 0)
  val0 + val1 + val2
