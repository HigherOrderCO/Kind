type Bits {
  e,
  o(pred: Bits),
  i(pred: Bits),
}

Bits.inc(a: Bits): Bits
  case a {
    e: Bits.i(Bits.e),
    o: Bits.i(a.pred),
    i: Bits.o(Bits.inc(a.pred)),
  }

Bits.add(a: Bits, b: Bits): Bits
  case b {
    e: a,
    o: case a {
      e: b,
      o: Bits.o(Bits.add(a.pred, b.pred)),
      i: Bits.i(Bits.add(a.pred, b.pred)),
    },
    i: case a {
      e: b,
      o: Bits.i(Bits.add(a.pred, b.pred)),
      i: Bits.o(Bits.add(Bits.inc(a.pred), b.pred))
    }
  }

Bits.eql(a: Bits, b: Bits): Bool
  case a {
    e: case b {
      e: Bool.true,
      o: Bool.false,
      i: Bool.false,
    },
    o: case b {
      e: Bool.false,
      o: Bits.eql(a.pred, b.pred),
      i: Bool.false,
    },
    i: case b {
      e: Bool.false,
      o: Bool.false,
      i: Bits.eql(a.pred, b.pred),
    }
  }

Bits.tail(a: Bits): Bits
  case a {
    e: Bits.e,
    o: a.pred,
    i: a.pred,
  }

Bits.show(a: Bits): String
  case a {
    e: "",
    o: String.cons('0', Bits.show(a.pred)),
    i: String.cons('1', Bits.show(a.pred)),
  }

Bits.reverse(a: Bits): Bits
  Bits.reverse.tco(a, Bits.e)

Bits.reverse.tco(a: Bits, r: Bits): Bits
  case a {
    e: r,
    o: Bits.reverse.tco(a.pred, Bits.o(r)),
    i: Bits.reverse.tco(a.pred, Bits.i(r))
  }

Bits.concat(a: Bits, b: Bits): Bits
  case a {
    e: b,
    o: Bits.o(Bits.concat(a.pred, b)),
    i: Bits.i(Bits.concat(a.pred, b))
  }

Bits.chunks_of.go(
  len   : Nat,  // length of each chunk
  bits  : Bits, // bits to be split
  need  : Nat,  // number of vals to complete chunk
  chunk : Bits  // current chunk
) : List(Bits)
  case bits {
    e: List.cons<_>(Bits.reverse(chunk), List.nil<_>),
    o: case need {
      zero: 
        let head = Bits.reverse(chunk);
        let tail = Bits.chunks_of.go(len, bits, len, Bits.e);
        List.cons<_>(head, tail),
      succ:
        let chunk = Bits.o(chunk);
        Bits.chunks_of.go(len, bits.pred, need.pred, chunk)
    },
    i: case need {
      zero:
        let head = Bits.reverse(chunk);
        let tail = Bits.chunks_of.go(len, bits, len, Bits.e);
        List.cons<_>(head, tail),
      succ:
        let chunk = Bits.i(chunk);
        Bits.chunks_of.go(len, bits.pred, need.pred, chunk)
    }
  }

Bits.chunks_of(len: Nat, bits: Bits): List(Bits)
  Bits.chunks_of.go(len, bits, len, Bits.e)

Bits.to_nat(b: Bits): Nat
  case b {
    e: 0,
    o: Nat.mul(2, Bits.to_nat(b.pred)),
    i: Nat.succ(Nat.mul(2, Bits.to_nat(b.pred)))
  }

Bits.from_string(str: String): Bits
  case str{
    nil: Bits.e
    cons: case U16.eql(str.head, Char.parse("1")){
      true : Bits.i(Bits.from_string(str.tail))
      false: Bits.o(Bits.from_string(str.tail))
      // TODO: what is this from past code? 
      //| Unit.new
    }
  }

// Bits.parse_hex: String -> Bits
//   (str) Bits.parse_hex.tco(str, Bits.nil)

// Bits.parse_hex.tco(str: String, res: Bits): Bits
//   case str{
//     nil: res
//     cons: let b0000 = Bits.o(Bits.o(Bits.o(Bits.o(Bits.e))))
//       let bhead = Nat.apply<Bits>(Char.hex_value(str.head), Bits.inc, b0000)
//       Bits.parse_hex.tco(str.tail, Bits.concat(bhead, res))
//   }


Bits.length(xs: Bits): Nat
  Bits.length.go(xs, 0)

Bits.length.go(xs: Bits, n: Nat): Nat
  case xs {
    e: n,
    o : Bits.length.go(xs.pred, Nat.succ(n)),
    i : Bits.length.go(xs.pred, Nat.succ(n))
  }

Bits.slice(len: Nat, bits: Bits): Bits
  case len {
    zero: Bits.e,
    succ: case bits {
      e: Bits.e,
      o : Bits.o(Bits.slice(len.pred, bits.pred)),
      i : Bits.i(Bits.slice(len.pred, bits.pred))
    }
  }

// Converts a bitstring to a string
Bits.to_string(bits: Bits): String
  case bits {
    e: "",
    o : String.concat("0", Bits.to_string(bits.pred)),
    i : String.concat("1", Bits.to_string(bits.pred))
  }

// Converts a bitstring to a U32 value
Bits.to_u32(bits: Bits): U32
  U32.new(Word.from_bits(32, bits))

// Multiplies two bitstrings.
Bits.mul(a: Bits, b: Bits): Bits
  Bits.mul.go(a, b, Bits.e)

Bits.mul.go(a: Bits, b: Bits, bits: Bits): Bits
  case b {
    e: bits,
    o : Bits.mul.go(Bits.o(a), b.pred, bits),
    i : Bits.mul.go(Bits.o(a), b.pred, Bits.add(a, bits))
  }

// TODO Divides two bitstrings.
Bits.div(a: Bits, b: Bits): Bits
  Bits.div(a, b)

// TODO Modulus of two Bitstrings.
Bits.mod(a: Bits, b: Bits): Bits
  Bits.mod(a, b)

// TODO
// Subtracts two Bitstrings.
Bits.sub(a: Bits, b: Bits): Bits
  Bits.sub(a, b)
